 1  REM DEPLOY PROGRAMS AS RELOCATABLE BINARIES
 2  LOMEM: 27904
 3  HIMEM: 32767
 4  REM SAVE THIS PROGRAM ENDING ADDRESS
 5  LB0% = PEEK (175): HB0% = PEEK (176)
 10  PRINT "INSERT TOWN SOURCE D1": GET A$
 20  PRINT "INSERT TOWN DEPLOY D2": GET A$
 30  PRINT "PRESS KEY TO BEGIN CONVERSION": GET A$
 40  TEXT: HOME
 50  FOR I = 1 TO 14
 60  READ F$
 70  PRINT "PROCESSING ";F$
 75  REM NOT SURE IF THE FOLLOWING LINE WILL TRIP INTERPRETER
 80  POKE 103,0: POKE 104,64: PRINT : PRINT CHR$(4);"LOAD ";F$;",D1": POKE 103,1: POKE 104,8
 90  LB%=PEEK(175): HB%=PEEK(176): POKE 175,LB0%: POKE 176,HB0%
 100  GOSUB 1000
 110  PRINT "LENGTH ";L%
 120  PRINT: PRINT CHR$(4);"BSAVE ";F$;",A$4000,L";L%;",D2"
 130  NEXT I
 999  END

 1000  REM  DECODE LENGTH
 1010  ST% = 16384
 1020  EN% = LB% + HB%*256
 1030  L% = EN% - ST%
 1040  RETURN

 10000  DATA  "OUTSIDE"
 10010  DATA  "TOWN"
 10020  DATA  "PUB"
 10030  DATA  "FOOD"
 10040  DATA  "LIBRARY"
 10050  DATA  "TEMPLE"
 10060  DATA  "WEAPARM"
 10070  DATA  "SHIPYARD"
 10080  DATA  "ALCHEMIST"
 10090  DATA  "ARCHWIZ"
 10100  DATA  "SAGE"
 10110  DATA  "TCOMBAT"
 10120  DATA  "CHAIN"
 10130  DATA  "SAVE GAME"
