10  PRINT: PRINT CHR$(4);"MAXFILES 1"
20  CLEAR : RESTORE
30  LOMEM: 27904
40  HIMEM: 32767
50  GOSUB 8000 : REM GLOBAL ARRAYS
60  GOSUB 6300 : REM DRIVE MAPPINGS

100  REM ENTER THE GAME
110  TEXT : HOME
120  POKE 27893,0: POKE 27894,0: POKE 27895,0: REM ALTAR FLAGS
125  POKE 8187,0: REM WALKING
130  D = 0 : GOSUB 9000 : GOSUB 900 : REM GET PARTY LIST
140  NA$ = PN$(0) : REM EXPECT ONLY ONE AND IN FIRST POSITION
150  GOSUB 10000 : GOSUB 500 : REM LOAD DATA
290  PRINT : PRINT : PRINT : PRINT CHR$(4);"BLOAD OUTSIDE,A$4001"
299  POKE 16384,0: POKE 103,1: POKE 104,64: POKE 175,255: POKE 176,108: GOTO 0

500  REM LOAD
510  HOME: VTAB 10: PRINT TAB(20-LEN(NA$)/2);NA$
530  PRINT CHR$(4);"OPEN D:";NA$
535  PRINT CHR$(4);"READ D:";NA$
540  FOR L = 0 TO 3
541  INPUT NM$(L)
542  INPUT RA$(L)
543  INPUT CL$(L)
544  INPUT ZA%(L)
545  INPUT AB%(L)
546  INPUT AC%(L)
547  INPUT AF%(L)
548  INPUT AG%(L)
548  INPUT AH%(L)
549  INPUT AI%(L)
550  INPUT AJ%(L)
551  INPUT AK%(L)
552  INPUT DV$(L)
553  INPUT DW$(L)
554  INPUT DX$(L)
555  INPUT WT%(L)
556  FOR M = 0 TO 7: INPUT AP$(L,M): NEXT
557  FOR M = 0 TO 15: INPUT AX$(L,M): NEXT
560  NEXT L
565  INPUT X
566  INPUT Y
570  INPUT AD%: INPUT AE: INPUT RM$
575  PRINT CHR$(4);"CLOSE D:";NA$
580  PRINT CHR$(4);"BLOAD CK:";NA$;",A$6CF0"
585  PRINT CHR$(4);"BLOAD MAP:";NA$;",A$8000"
600  FOR L = 0 TO 3:WU(L) = 1
610  IF CL$(L) = "WIZARD" THEN WU(L) = 2
620  IF CL$(L) = "CLERIC" THEN WU(L) = 1.8
630  IF CL$(L) = "THIEF" THEN WU(L) = 1.5
640  IF CL$(L) = "MONK" THEN WU(L) = 1.3
650  IF CL$(L) = "RANGER" THEN WU(L) = 1.3
660  IF CL$(L) = "PALADIN" THEN WU(L) = 1.1
670  NEXT L
699  RETURN

900  REM LOAD LIST
901  GOSUB 9900
905  PRINT : PRINT : PRINT : PRINT CHR$(4);"OPEN PLIST"
910  PRINT CHR$(4);"READ PLIST"
925  FOR I = 0 TO 9
930  INPUT PN$(I)
935  NEXT I
940  PRINT CHR$(4);"CLOSE PLIST"
949  RETURN

6300  REM READ DRIVES
6310  PRINT : PRINT : PRINT : PRINT CHR$(4);"OPEN DD"
6320  PRINT CHR$(4);"READ DD"
6330  FOR I = 0 TO 3
6340  INPUT DL$(I)
6350  INPUT WS%(I)
6360  INPUT WD%(I)
6370  INPUT WV%(I)
6380  NEXT I
6390  PRINT CHR$(4);"CLOSE DD"
6399  RETURN

8000  REM GLOBALS
8010  DIM PN$(9)
8020  DIM WS%(3): DIM WD%(3): DIM WV%(3): REM SLOT DRIVE VOLUME
8030  DIM DL$(3): REM DISK LABELS
8040  DIM WU(3)
8050  DIM NM$(3): DIM RA$(3)
8060  DIM CL$(3): DIM ZA%(3)
8070  DIM AB%(3): DIM AC%(3)
8080  DIM AF%(3): DIM AG%(3)
8090  DIM AH%(3): DIM AI%(3)
8100  DIM AJ%(3): DIM AK%(3)
8110  DIM DV$(3): DIM DW$(3)
8120  DIM DX$(3): DIM WT%(3)
8130  DIM B$(3): DIM PM%(9)
8140  DIM PC%(3): DIM GP%(3)
8150  DIM AX$(3,15): DIM AP$(3,7)
8190  REM POKE IN STACK FIXER
8191  POKE 768,104 : POKE 769,168 : POKE 770,104 : POKE 771,166 : POKE 772,223 : POKE 773,154 : POKE 774,72 : POKE 775,152 : POKE 776,72 : POKE 777,96
8199  RETURN

9000  REM VERIFY DISK
9110  ONERR  GOTO 9200
9120  PRINT : PRINT : PRINT
9130  PRINT  CHR$(4);"VERIFY DISK";D;",S";WS%(D);",D";WD%(D);",V";WV%(D)
9140  POKE 216,0 : RETURN
9200  HOME : PRINT "INSERT ";DL$(D);" DISK IN D";WD%(D);"- ";: GET A$
9210  CALL 768
9220  PRINT : PRINT : PRINT : RESUME

9900  REM VERIFY PARTY LIST
9910  ONERR  GOTO 9950
9920  PRINT : PRINT : PRINT : PRINT CHR$(4);"VERIFY PLIST"
9930  RETURN
9950  HOME : PRINT "THERE IS NO DEFAULT PARTY."
9955  PRINT "USE CHARACTER DISK TO CREATE."
9960  CALL -3288: REM CLEAR THE STACK
9970  POKE 216,0
9980  END

10000  REM VERIFY PARTY
10110  ONERR  GOTO 10200
10120  PRINT : PRINT : PRINT
10130  PRINT  CHR$(4);"VERIFY CK:";NA$
10140  POKE 216,0: RETURN
10200  HOME : PRINT "'";NA$;"' NOT ON THIS DISK. "
10210  CALL -3288: REM CLEAR THE STACK
10220  POKE 216,0
10230  PRINT "DISK IS CORRUPTED."
10240  PRINT "WE EXPECTED TO FIND ";NA$;" BUT THEY ARE NOT HERE."
10250  END
