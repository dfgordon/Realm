10 REM CREATE A SCRIPT TO DEPLOY REALM DISK 0
15 DIM D%(4): NU=17
20 Q$=CHR$(34)
25 INPUT "CLEAN D2: ";A$
26 INPUT "FILE (ENTER=ALL): ";F0$
30 IF A$="Y" THEN GOSUB 600
40 RESTORE
50 PRINT CHR$(4);"DELETE DS,D1"
60 PRINT CHR$(4);"OPEN DS"
70 PRINT CHR$(4);"WRITE DS"
80 FOR I = 1 TO NU
90 READ F$,L$
95 IF F$<>F0$ AND F0$<>"" THEN 120
100 IF L$ <> "0000" THEN GOSUB 400
110 IF L$ = "0000" THEN GOSUB 500
120 NEXT I
130 PRINT CHR$(4);"CLOSE DS"
140 PRINT "FINISHED. NOW TYPE EXEC DS.": END

400 GOSUB 1000: AD% = L%: AL% = LB%: AH% = HB%
405 PRINT "POKE ";AD%-1;",0"
410 PRINT "POKE 103,";AL%
420 PRINT "POKE 104,";AH%
430 PRINT "LOAD ";F$;",D1"
440 PRINT "LB% = PEEK(175)"
450 PRINT "HB% = PEEK(176)"
460 PRINT "L% = 256*HB% + LB% - ";AD%
470 S1$ = "BSAVE "+F$+",A"+STR$(AD%)+",L"
480 PRINT "SV$ = ";Q$;S1$;Q$;"+STR$(L%)+";Q$;",D2";Q$
490 PRINT "PRINT CHR$(4);SV$"
498 PRINT "NEW"
499 RETURN

500 PRINT "LOAD ";F$;",D1"
510 PRINT "SAVE ";F$;",D2"
520 PRINT "NEW"
530 RETURN

600 RESTORE
610 FOR I = 1 TO NU
620 READ F$,L$
630 PRINT : PRINT CHR$(4);"DELETE ";F$;",D2"
640 NEXT I
650 RETURN

1000  REM  DECODE WORD
1020  FOR J = 1 TO 4
1030 D%(J) =  VAL ( MID$ (L$,J,1))
1040  IF D%(J) = 0 AND  MID$ (L$,J,1) <  > "0" THEN  GOSUB 2000
1050  NEXT J
1060 LB% = D%(3) * 16 + D%(4)
1070 HB% = D%(1) * 16 + D%(2)
1080 L% = LB% + HB% * 256
1090  RETURN

2000  REM  CONVERT HEX LETTER
2010 D%(J) =  ASC ( MID$ (L$,J,1)) -  ASC ("A") + 10
2020  RETURN

10000 DATA "ALCHEMIST","0801","ARCHWIZ","0C01"
10010 DATA "CHAIN","1001","FOOD","0C01","LIBRARY","0801","OUTSIDE","4001"
10020 DATA "PUB","0C01","SAGE","0C01","SHIPYARD","0C01","TCOMBAT","4001"
10030 DATA "TEMPLE","0801","TOWN","4001","WEAPARM","0801"
10040 DATA "SAVE GAME","0801"

11000 DATA "AUTOSTART","0000","GUTEN TAG","0000","DISK0","0000"
