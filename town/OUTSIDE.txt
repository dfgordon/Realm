 0  PRINT CHR$(4);"PR#0"
 1  ROT=0: SCALE=1: POKE 232,0: POKE 233,10: REM BRUSHES
 2  PRINT CHR$(4);"BLOAD S2"
 3  PRINT CHR$(4);"BLOAD OUTSPS"
 4  PRINT CHR$(4);"BLOAD OUTSIDE.INTRP"
 5  PRINT CHR$(4);"BLOAD MRC3"
 6  PRINT CHR$(4);"BLOAD SDP.INTRP,A$1100"
 10  POKE 6399,5: IF RM$="ABYSS" THEN POKE 6399,2
 13  PRINT CHR$(4);"BLOAD FRAMEO,A$8C80"
 14  POKE 8189,128: POKE 8188,0
 15  POKE 7680,173: POKE 7683,96
 16  POKE 5089,0
 17  GOSUB 380: CALL 4352: GOSUB 381
 18  HOME : GOTO 100
 20  VTAB 20
 21  GET A$:AE = AE - .5: PRINT
 22  IF  PEEK (8187) = 32 THEN AE = AE + .3
 30  IF AE < 0 THEN 50000
 31  IF AE < 100 THEN 39
 38  GOTO 40
 39  INVERSE : PRINT "FOOD": NORMAL
 40  GOSUB 5100
 71  IF Y < 0 OR Y > 79 OR X < 0 OR X > 79 THEN 106
 73  IF A$ = "Z" THEN 4000
 74  IF A$ = "B" THEN 500
 75  IF A$ = "X" THEN 600
 76  IF A$ = "C" THEN 700
 78  IF A$ = "R" THEN 2800
 79  IF A$ = "Q" THEN 10000
 80  IF A$ = "E" THEN 8000
 81  IF A$ = "I" THEN 2000
 82  IF A$ = CHR$(19) THEN 7050
 83  IF A$ = "U" THEN 1000
 84  IF A$ = "L" THEN  POKE 8187,0: HOME
 85  IF A$ = "F" THEN 13000
 100 GOSUB 5010
 102  IF  PEEK (8186) = 16 THEN 106
 104  GOTO 20
 106  X = X - XK: Y = Y - YK
 108  POKE 768,169: POKE 813,5: POKE 769,30: CALL 768: GOTO 20

 380  POKE 768,189: POKE 769,128: POKE 770,140: POKE 771,96: RETURN
 381  POKE 768,169: POKE 770,162: POKE 771,16: RETURN

 500  GOSUB 5001
 510  IF  PEEK (768) = 11 THEN 520
 511  GOTO 8070
 520  POKE 8187,16: POKE 768,169
 530  N = 4: GOSUB 5201
 540  HOME : GOTO 100

 600  GOSUB 5001
 610  IF  PEEK (8187) = 16 AND PEEK(768) = 4 THEN 630
 620  GOTO 8070
 630  POKE 8187,0
 640  N = 11: GOSUB 5201
 650  HOME : GOTO 100

 700  HOME : VTAB 21: PRINT "WHO WILL CAST (#) ";: GET A$:A =  VAL (A$) - 1
 701  IF A < 0 OR A > 3 THEN 700
 703  IF AB%(A) = 0 THEN 700
 704  IF DX$(A) = "" THEN 730
 705 B$ = DX$(A)
 706  PRINT
 720  IF B$ = "RAISE DEAD" THEN 750
 721  IF B$ = "CURE 1" THEN 760
 722  IF B$ = "CURE 2" THEN 765
 723  IF B$ = "CURE 3" THEN 770
 724  IF B$ = "FLY" THEN 790
 725  IF B$ = "ASTRAL SPELL" AND RM$ = "WORNOTH" THEN 800
 730  GOTO 2910

 747  GOSUB 30050: HOME : GOTO 100

 750  HOME: VTAB 21: PRINT "RAISE WHOM (#) ";: GET A$
 751 B =  VAL (A$) - 1: IF B < 0 OR B > 3 THEN 750
 752  IF AB%(B) > 0 THEN HOME: VTAB 21: PRINT NM$(B);" IS NOT DEAD! ";: GOTO 747
 753  IF B$ <> "RAISE DEAD" THEN GOSUB 2200
 754  IF B$ = "RAISE DEAD" THEN GOSUB 2300
 755 AB%(B) = 10: HOME : VTAB 21: PRINT "-CASTING RAISE DEAD-": GOSUB 30000
 756  PRINT NM$(B);" IS RAISED! ";: GET A$: HOME : GOTO 100

 760 CA = 50: GOTO 780
 765 CA = 100: GOTO 780
 770 CA = 200
 780 HOME: VTAB 21: PRINT "CURE WHOM (#) ";: GET A$
 781 B =  VAL (A$) - 1: IF B < 0 OR B > 3 THEN 780
 782  IF AB%(B) < 1 THEN HOME: VTAB 21: PRINT NM$(B);" IS DEAD! ";: GOTO 747
 783  IF LEFT$(B$,2) = "AM" THEN GOSUB 2200
 784  IF LEFT$(B$,2) = "CU" THEN GOSUB 2300
 785  HOME : VTAB 21: PRINT "-CASTING CURE-": GOSUB 30000
 786 C =  INT (CA *  RND (1)) + CA
 787 AB%(B) = AB%(B) + C: IF AB%(B) > AC%(B) THEN AB%(B) = AC%(B)
 788  PRINT C;" POINTS CURED. ";: GET A$: HOME : GOTO 100

 790  IF  PEEK (8187) = 16 THEN 8070
 799  PRINT "-CASTING FLY-": POKE 8187,32: GOSUB 30000: HOME: GOTO 100

 800  PRINT "-CASTING ASTRAL SPELL-": GOSUB 30000: POKE 6399,20: POKE 6403,20: POKE 6435,20: HOME: GOTO 100

 1000  HOME : VTAB 21: PRINT "WHO WILL USE (#) ";: GET A$
 1001 A =  VAL (A$) - 1: IF A < 0 OR A > 3 THEN 1000
 1004  IF AB%(A) = 0 THEN 1000
 1005 B$ = DW$(A)
 1010  IF B$ = "POTION OF FLYING" THEN 790
 1015  IF B$ = "ROD OF RESURRECTION" THEN 750
 1020  IF B$ = "AMULET OF HEALING" THEN 770
 1023  GOTO 2900

 2000  TEXT: HOME : PRINT "--INVENTORY SWAP--"
 2001  HTAB 1: VTAB 3: PRINT "FIRST SWAPPER (#) ";: GET A$
 2002 A =  VAL (A$) - 1: IF A < 0 OR A > 3 THEN 2001
 2005  HTAB 1: VTAB 5: PRINT "OTHER SWAPPER (#) ";: GET A$
 2006 B =  VAL (A$) - 1: IF B < 0 OR B > 3 THEN 2005
 2010 M = A: HOME: GOSUB 4210
 2011  PRINT "FIRST SWAP ITEM (#) ";: GET A$
 2012 N =  VAL (A$) - 1: IF N < 0 OR N > 7 THEN 2099
 2020 M = B: HOME: GOSUB 4210
 2021 PRINT "SECOND SWAP ITEM (#)";: GET A$
 2022 L =  VAL (A$) - 1: IF L < 0 OR L > 7 THEN 2099
 2030 B$ = AP$(A,N)
 2040 AP$(A,N) = AP$(B,L)
 2050 AP$(B,L) = B$
 2060  GOSUB 7100
 2070  A = B: GOSUB 7100
 2099  POKE  - 16304,0: HOME : GOTO 100

 2200 IF ZA%(A) / 1000 + AH%(A) >  INT (99 *  RND (1)) + 1 THEN RETURN
 2202 I = 0
 2204  IF AP$(A,I) = DW$(A) THEN 2208
 2206 I = I + 1: GOTO 2204
 2208 AP$(A,I) = ""
 2210 I = I + 1: IF I > 7 THEN 2216
 2212  IF AP$(A,I) = DW$(A) THEN  RETURN
 2214  GOTO 2210
 2216  INVERSE : PRINT DW$(A);" IS EXPENDED ": NORMAL :DW$(A) = "HA" + "NDS"
 2220  FOR P = 1 TO 500: NEXT P: RETURN

 2250 I = 0
 2252  IF AP$(A,I) = A$ THEN 2256
 2254 I = I + 1: IF I<8 THEN 2252
 2256  IF I=8 THEN RETURN
 2258  IF DW$(A) = A$ THEN DW$(A) = "HA" + "NDS"
 2260 AP$(A,I) = "": RETURN

 2300 IF ZA%(A) / 1000 + AH%(A) >  INT (99 *  RND (1)) + 1 THEN RETURN
 2302 I = 0
 2304  IF AX$(A,I) = DX$(A) THEN 2308
 2306 I = I + 1: GOTO 2304
 2308 AX$(A,I) = ""
 2310 I = I + 1: IF I > 15 THEN 2316
 2312  IF AX$(A,I) = DX$(A) THEN  RETURN
 2314  GOTO 2310
 2316  INVERSE : PRINT DX$(A);" IS EXHAUSTED ": NORMAL :DX$(A) = ""
 2320  FOR P = 1 TO 500: NEXT P: RETURN

 2800  HOME : VTAB 21: PRINT "READY WHO (#) ";: GET A$
 2801  A =  VAL (A$) - 1: IF A<0 OR A>3 THEN 2800
 2802  IF AB%(A)=0 THEN 2800
 2810  PRINT:PRINT "READY WHAT? ";: GET A$
 2811  HOME : VTAB 21
 2820  IF A$ = "W" THEN 2830
 2821  IF A$ = "A" THEN 2850
 2822  IF A$ = "S" THEN 2870
 2825  GOTO 2810

 2830  TEXT:HOME: M = A: GOSUB 4210: PRINT "WHAT WEAPON:";:GET A$
 2831  L = VAL(A$) - 1: IF L<0 OR L>7 THEN 2830
 2840  GOSUB 2950: IF M THEN GOTO 2900
 2841  GOSUB 2920: IF M THEN GOTO 2900
 2849  DW$(A) = AP$(A,L): GOTO 2889

 2850  TEXT:HOME: M = A: GOSUB 4210: PRINT "WHAT ARMOUR:";:GET A$
 2851  L = VAL(A$) - 1: IF L<0 OR L>7 THEN 2850
 2860  GOSUB 2950: IF M THEN GOTO 2900
 2861  GOSUB 2920: IF NOT M THEN GOTO 2900
 2869  DV$(A) = AP$(A,L): GOTO 2889

 2870  TEXT:HOME: M = A: GOSUB 4310: PRINT "WHAT SPELL:";:GET A$
 2871  L = VAL(A$) - 1: IF L<0 OR L>15 THEN L = ASC(A$) - 56
 2872  IF L<0 OR L>15 THEN 2870
 2880  DX$(A) = AX$(A,L)
 2889  POKE -16304,0 : HOME : GOTO 100

 2900  HOME : VTAB 21: PRINT "INVALID ITEM! ";: GOSUB 30050: GOTO 2889
 2910  HOME : VTAB 21: PRINT "INVALID SPELL! ";: GOSUB 30050: GOTO 2889

 2920  A$ = LEFT$(AP$(A,L),3)
 2930  M = (A$="LEA" OR A$="CHA" OR A$="PLA")
 2940  RETURN

 2950  A$ = LEFT$(AP$(A,L),3)
 2951  M = (CL$(A)="WIZARD" OR CL$(A)="THIEF" OR CL$(A)="MONK") AND (A$="CHA" OR A$="PLA")
 2952  M = M OR (CL$(A)<>"CLERIC" AND (A$="ROD" OR A$="AMU" OR A$="LIG" OR A$="FIR"))
 2953  M = M OR (CL$(A)<>"WIZARD" AND (A$="WAN" OR A$="STA"))
 2954  RETURN

 4000  TEXT : HOME : PRINT "STATUS FOR WHO (#) ";: GET A$
 4010 M =  VAL (A$) - 1
 4011  IF M > 3 THEN 4000
 4020  IF M < 0 THEN 4000
 4050  HOME
 4060  PRINT "NAME: ";NM$(M)
 4070  PRINT "CLASS: ";CL$(M)
 4080  PRINT "RACE: ";RA$(M)
 4090  PRINT "MAXIMUM HIT POINTS: ";AC%(M)
 4100  PRINT "CURRENT HIT POINTS: ";AB%(M)
 4110  PRINT "GOLD COINS: ";AD%
 4120  PRINT "FOOD SUPPLY: "; INT (AE)
 4130  PRINT "STRENGTH: ";AF%(M)
 4140  PRINT "INTELLIGENCE: ";AG%(M)
 4150  PRINT "WISDOM: ";AH%(M)
 4160  PRINT "AGILITY: ";AI%(M)
 4170  PRINT "STAMINA: ";AJ%(M)
 4180  PRINT "CHARISMA: ";AK%(M)
 4181  PRINT "EXPERIENCE: ";ZA%(M)
 4182  PRINT "LEVEL: "; INT (ZA%(M) / 1000) + 1
 4183  PRINT "MAXIMUM LEVEL: ";WT%(M)
 4184  PRINT : PRINT "WEAPON READY: ";DW$(M)
 4185  PRINT "ARMOUR READY: ";DV$(M)
 4186  PRINT "SPELL READY: ";DX$(M)
 4190  GET A$
 4200  HOME
 4201  GOSUB 4210: GET B$
 4202  HOME : GOSUB 4310
 4203  GET B$
 4205  HOME : POKE  - 16304,0: GOTO 20
 4210  PRINT "ITEMS:"
 4220  FOR I = 0 TO 7
 4221  PRINT I + 1;". ";
 4230  PRINT AP$(M,I)
 4290  NEXT
 4305  RETURN
 4310  PRINT "SPELLS:"
 4320  FOR I = 0 TO 15
 4321  PRINT I + 1;". ";
 4330  PRINT AX$(M,I)
 4390  NEXT
 4510  RETURN

 5001  POKE 8189,128: POKE 8188,0: GOSUB 5050: POKE 4,0: POKE 5,0: CALL 7076: RETURN

 5010  POKE 8189,128: POKE 8188,0
 5011  POKE 4,72: POKE 5,22
 5012  IF  PEEK (8187) = 0 THEN  CALL 925
 5013  GOSUB 5050: CALL 6384: RETURN

 5050  POKE 8190, INT (X / 2): POKE 8191,Y
 5060  POKE 8185,1
 5070  IF (X / 2) =  INT (X / 2) THEN POKE 8185,0
 5080  RETURN

 5100  XK = 0: YK = 0
 5110  IF A$ =  CHR$ (11) OR A$ = "Y" THEN YK = - 1
 5120  IF A$ =  CHR$ (8) OR A$ = "G" THEN XK = - 1
 5130  IF A$ =  CHR$ (21) OR A$ = "J" THEN XK = 1
 5140  IF A$ =  CHR$ (10) OR A$ = " " THEN YK = 1
 5141  IF A$ = "-" THEN XK=-1: YK=-1
 5142  IF A$ = "=" THEN XK=1: YK=-1
 5143  IF A$ = "[" THEN XK=-1: YK=1
 5144  IF A$ = "]" THEN XK=1: YK=1
 5150  X = X + XK: Y = Y + YK
 5160  RETURN

 5201 GOSUB 5001: BY = 32768
 5202 BY = BY + (Y*40) + INT(X/2)
 5210 Z = 16: IF (X/2)=INT(X/2) THEN Z=1
 5220 POKE BY,PEEK(BY) + N*Z - PEEK(768)*Z: RETURN

 7000 A$ = "": HTAB 1: PRINT ">";
 7001  GET B$
 7002  IF B$ =  CHR$ (13) THEN  RETURN
 7003  IF B$ =  CHR$ (8) THEN 7009
 7004  IF B$ =  CHR$ (10) OR B$ =  CHR$ (11) OR B$ =  CHR$ (21) THEN 7001
 7005  IF  LEN (A$) > 37 THEN 7001
 7006  PRINT B$;:A$ = A$ + B$: GOTO 7001
 7009  IF  LEN (A$) < 2 THEN 7000
 7010 A$ =  LEFT$ (A$, LEN (A$) - 1): POKE 36, PEEK (36) - 1: CALL  - 868: GOTO 7001

 7050  HOME : VTAB 21: IF  PEEK (921) = 96 THEN 7080
 7060  POKE 921,96: PRINT "SOUND OFF"
 7061  FOR P = 1 TO 500: NEXT P: HOME : GOTO 20
 7080  POKE 921,141: PRINT "SOUND ON": GOTO 7061

 7100 N = 0
 7105  FOR M = 0 TO 7: IF DW$(A) = AP$(A,M) THEN N = 8
 7106  NEXT : IF N = 0 THEN DW$(A) = "HA"+"NDS"
 7109 N = 0
 7110  FOR M = 0 TO 7: IF DV$(A) = AP$(A,M) THEN N = 8
 7111  NEXT : IF N = 0 THEN DV$(A) = "SK"+"IN"
 7115  RETURN

 8000  GOSUB 5001
 8010  HOME : VTAB 21
 8020 B$ = "": M = PEEK(768)
 8030  IF  PEEK (8187) = 16 THEN 8070
 8040  IF M=7 OR M=12 OR M=14 THEN 8100
 8050  IF M=9 OR M=13 THEN 8200
 8060  IF M=0 THEN 8300
 8070  PRINT "NOT HERE. ";: GOSUB 30050: HOME : GOTO 100

 8100  PG$ = "TO"+"WN": R%=0
 8110  PRINT CHR$(4);"BLOAD CHAIN
 8120  POKE 103,0: POKE 104,16: GOTO 10

 8200  PG$ = "DUN"+"GEON": R% = 0
 8210  GOTO 8110

 8300  IF X = 3 THEN M = 27888: XT = 30: YT = 59: B$ = "FON" + "KRAKIS"
 8301  IF X = 71 THEN M = 27889: XT = 37: YT = 66: B$ = "AB" + "YSS"
 8302  IF X = 10 THEN M = 27890: XT = 34: YT = 74: B$ = "WOR" + "NOTH"
 8303  IF RM$<>"ARRINEA" THEN B$ = "ARR" + "INEA"
 8304  IF RM$="FONKRAKIS" THEN XT = 3: YT = 56: GOTO 8350
 8305  IF RM$="ABYSS" THEN XT = 71: YT = 38: GOTO 8350
 8306  IF RM$="WORNOTH" THEN XT = 10: YT = 14: GOTO 8350
 8310  IF PEEK(M)=0 THEN 8400
 8320  IF PEEK(M)=8 THEN 8500
 8350  D = 0: GOSUB 9000
 8351  IF RM$="ARRINEA" THEN PRINT:PRINT CHR$(4);"BSAVE ARRINEA,A$8000,L3200": D = 1: GOSUB 9000
 8352  RM$ = LEFT$(B$+" ",LEN(B$)): A$ = "RIDING THE RAYS : "+RM$
 8360  HOME: VTAB 21: PRINT TAB(20-LEN(A$)/2);A$
 8370  PRINT: PRINT CHR$(4);"BLOAD ";RM$
 8380  POKE 6399,5: IF RM$ = "ABYSS" THEN  POKE 6399,2
 8390  X = XT: Y = YT: HOME: GOTO 100

 8400  PRINT "ONE OF THE ANCIENT PYRAMID GATES!"
 8410  GOSUB 8600: HOME: VTAB 21
 8420  IF N = 0 THEN PRINT "YOU DON'T HAVE THE RIGHT KEY.";: GOSUB 30050: HOME: GOTO 100
 8430  PRINT "YOU USE THE ";A$;"...";: GET A$: POKE M,8

 8500  HOME : VTAB 21: PRINT "YOU ARE INSIDE!": PRINT "AN INSCRIPTION READS: ";B$
 8510  GOSUB 8650: HOME: VTAB 21
 8520  IF N = 0 THEN PRINT "YOU DON'T HAVE THE RIGHT PRISM.";: GOSUB 30050: HOME: GOTO 100
 8530  PRINT "YOU USE THE ";A$;"...";: GET A$: POKE M,16: GOTO 8350

 8600  N = 0: FOR A = 0 TO 3: FOR L = 0 TO 7
 8605  A$ = AP$(A,L)
 8610  IF A$ = "PLATINUM KEY" AND B$ = "FONKRAKIS" THEN N = 1: RETURN
 8611  IF A$ = "GOLD KEY" AND B$ = "ABYSS" THEN N = 1: RETURN
 8612  IF A$ = "SILVER KEY" AND B$ = "WORNOTH" THEN N = 1: RETURN
 8630  NEXT L: NEXT A: RETURN

 8650  N = 0: FOR A = 0 TO 3: FOR L = 0 TO 7
 8655  A$ = AP$(A,L)
 8660  IF A$ = "GREEN PRISM" AND B$ = "FONKRAKIS" THEN N = 1: RETURN
 8661  IF A$ = "RED PRISM" AND B$ = "ABYSS" THEN N = 1: RETURN
 8662  IF A$ = "CLEAR PRISM" AND B$ = "WORNOTH" THEN N = 1: RETURN
 8670  NEXT L: NEXT A: RETURN

 9000  REM VERIFY DISK
 9110  ONERR  GOTO 9200
 9120  PRINT : PRINT : PRINT
 9130  PRINT  CHR$(4);"VERIFY DISK";D;",S";WS%(D);",D";WD%(D);",V";WV%(D)
 9140  POKE 216,0 : RETURN
 9200  HOME : VTAB 21: PRINT "INSERT ";DL$(D);" DISK IN D";WD%(D);"- ";: GET A$
 9210  CALL 938
 9220  PRINT : PRINT : PRINT : RESUME

 10000  HOME : VTAB 21: IF  PEEK (8187) > 0 OR RM$<>"ARRINEA" THEN 8070
 10001  ONERR  GOTO 10003
 10002  IF WV% = 2 THEN 10005
 10003  HOME : VTAB 21: PRINT "INSERT MASTER DISK- ";: GET A$
 10005  PRINT : PRINT : PRINT CHR$(4);"VERIFY TDISK,D1
 10008  PRINT CHR$(4);"BLOAD SAVE GAME
 10010  POKE 103,1: POKE 104,8: GOTO 0

 13000  VTAB 21: PRINT "FOOD": PRINT "    ";: HTAB 1: PRINT  INT (AE): PRINT "GOLD": PRINT AD%;
 13005  FOR I = 0 TO 3: VTAB 21 + I: HTAB 6: PRINT DW$(I);: HTAB 17: PRINT DX$(I);: HTAB 30: PRINT ZA%(I);: HTAB 36: PRINT AB%(I);
 13010  NEXT : GOTO 20

 30000  POKE 769,30: POKE 813,100: POKE 768,169: CALL 768: RETURN
 30050  POKE 8191,50: CALL 912: GET A$: RETURN
 50000  HOME : VTAB 21: PRINT "YOUR PARTY IS ELIMINATED! "
 50001  GOTO 50001
