 10  PRINT CHR$(4);"BLOAD TITLE.PIC,A$4000"
 20  HOME: POKE -16299,0: POKE  - 16304,0: POKE  - 16302,0: GET A$
 30  CLEAR : RESTORE : GOSUB 8000
 40  GOSUB 5300
 100  TEXT : HOME
 110  PRINT "SELECT ONE-"
 120  PRINT "1) SET DRIVES"
 130  PRINT "2) GENERATE PARTY"
 140  PRINT "3) DEPLOY PARTY
 150  PRINT "4) LIST PARTIES
 160  PRINT "5) RENAME PARTY
 170  PRINT "6) DELETE PARTY
 190  GET A$: A =  VAL (A$)
 195  ON A GOTO 5000,1000,6000,2000,3000,4000
 199  GOTO 100

 200  HOME
 210  PRINT "WHAT CLASS SHOULD ";NM$(L);" BE?"
 220  PRINT "1) FIGHTER"
 221  PRINT "2) WIZARD"
 222  PRINT "3) CLERIC"
 223  PRINT "4) THIEF"
 224  PRINT "5) MONK"
 225  PRINT "6) PALADIN"
 226  PRINT "7) RANGER"
 230  GET A$
 240  IF A$ = "1" THEN CL$(L) = "FIGHTER"
 241  IF A$ = "2" THEN CL$(L) = "WIZARD"
 242  IF A$ = "3" THEN CL$(L) = "CLERIC"
 243  IF A$ = "4" THEN CL$(L) = "THIEF"
 244  IF A$ = "5" THEN CL$(L) = "MONK"
 245  IF A$ = "6" THEN CL$(L) = "PALADIN"
 246  IF A$ = "7" THEN CL$(L) = "RANGER"
 247  IF CL$(L) = "" THEN 200
 248  RETURN

 250  HOME
 260  PRINT "WHAT RACE SHOULD ";NM$(L);" BE?"
 270  PRINT "1) HUMAN"
 271  PRINT "2) ELF"
 272  PRINT "3) DWARF"
 273  PRINT "4) HOBBIT"
 280  GET A$
 290  IF A$ = "1" THEN RA$(L) = "HUMAN"
 300  IF A$ = "2" THEN RA$(L) = "ELF"
 310  IF A$ = "3" THEN RA$(L) = "DWARF"
 320  IF A$ = "4" THEN RA$(L) = "HOBBIT"
 321  IF RA$(L) = "" THEN 250
 330  IF A$ = "2" AND CL$(L) = "CLERIC" THEN 350
 331  IF A$ = "2" AND CL$(L) = "PALADIN" THEN 350
 332  IF A$ = "2" AND CL$(L) = "THIEF" THEN 350
 333  IF A$ = "3" AND CL$(L) = "MONK" THEN 350
 334  IF A$ = "3" AND CL$(L) = "WIZARD" THEN 350
 335  IF A$ = "3" AND CL$(L) = "PALADIN" THEN 350
 336  IF A$ = "3" AND CL$(L) = "CLERIC" THEN 350
 337  IF A$ = "4" AND CL$(L) = "MONK" THEN 350
 338  IF A$ = "4" AND CL$(L) = "PALADIN" THEN 350
 339  IF A$ = "4" AND CL$(L) = "WIZARD" THEN 350
 340  IF A$ = "4" AND CL$(L) = "RANGER" THEN 350
 345  RETURN
 350  PRINT CL$(L);" CANNOT BE A ";RA$(L): GET A$: GOTO 250

 351  HTAB 1: VTAB 2: PRINT "1) STRENGTH: ";A%(0);TAB(38)
 352  HTAB 1: VTAB 3: PRINT "2) INTELLIGENCE: ";A%(1);TAB(38)
 353  HTAB 1: VTAB 4: PRINT "3) WISDOM: ";A%(2);TAB(38)
 354  HTAB 1: VTAB 5: PRINT "4) AGILITY: ";A%(3);TAB(38)
 355  HTAB 1: VTAB 6: PRINT "5) STAMINA: ";A%(4);TAB(38)
 356  HTAB 1: VTAB 7: PRINT "6) CHARISMA: ";A%(5);TAB(38)
 357  PRINT: RETURN

 360  FOR I = 0 TO 5: A%(I) = 12: NEXT I
 361  HOME: PRINT "ABILITIES:": GOSUB 351
 362  PRINT "ADJUST (Y/N) ?";: GET A$: IF A$="N" THEN 380
 363  PRINT: PRINT "ADJUST WHICH ABILITY: ";: GET A$: A = VAL(A$)-1: IF A<0 OR A>5 THEN 363
 364  PRINT: PRINT "COMPENSATE WITH ABILITY: ";: GET A$: B = VAL(A$)-1: IF B<0 OR B>5 OR A=B THEN 364
 370  HTAB 1: VTAB 12: PRINT "USE ARROWS, ENTER WHEN DONE.";: GET A$
 371  IF A$=CHR$(8) THEN Z = -1
 372  IF A$=CHR$(21) THEN Z = 1
 373  IF A$=CHR$(13) THEN 361
 375  A%(A) = A%(A) + Z: A%(B) = A%(B) - Z
 376  IF A%(A) < 5 OR A%(A) > 20 OR A%(B) < 5 OR A%(B) > 20 THEN Z = Z*-1: GOTO 375
 377  GOSUB 351: GOTO 370
 380  AF%(L) = A%(0): AG%(L) = A%(1): AH%(L) = A%(2): AI%(L) = A%(3): AJ%(L) = A%(4): AK%(L) = A%(5)
 381  RETURN

 400  REM PICK PARTY IN D
 410  HOME : PRINT "ENTER THE PARTY'S NAME-": GOSUB 7000
 420  NA$ = A$: IF NA$ = "" THEN 450
 430  GOSUB 9000 : GOSUB 10000
 440  RETURN
 450  CALL -3288
 460  GOTO 100

 500  REM LOAD
 510  PRINT : PRINT : PRINT : PRINT CHR$(4);"OPEN D:";NA$
 520  PRINT CHR$(4);"READ D:";NA$
 525  FOR L = 0 TO 3
 530  INPUT NM$(L)
 531  INPUT RA$(L)
 532  INPUT CL$(L)
 533  INPUT ZA%(L)
 534  INPUT AB%(L)
 535  INPUT AC%(L)
 536  INPUT AF%(L)
 537  INPUT AG%(L)
 538  INPUT AH%(L)
 539  INPUT AI%(L)
 540  INPUT AJ%(L)
 541  INPUT AK%(L)
 542  INPUT DV$(L)
 543  INPUT DW$(L)
 544  INPUT DX$(L)
 545  INPUT WT%(L)
 546  FOR M = 0 TO 7: INPUT AP$(L,M): NEXT
 547  FOR M = 0 TO 15: INPUT AX$(L,M): NEXT
 548  NEXT L
 565  INPUT X
 566  INPUT Y
 570  INPUT AD%: INPUT AE: INPUT FL%: INPUT RM$
 571  FOR I = 0 TO 5: INPUT FG%(I): NEXT I
 575  PRINT CHR$(4);"CLOSE D:";NA$
 585  PRINT CHR$(4);"BLOAD MAP:";NA$;",A$8000"
 600  FOR L = 0 TO 3:WU(L) = 1
 610  IF CL$(L) = "WIZARD" THEN WU(L) = 2
 620  IF CL$(L) = "CLERIC" THEN WU(L) = 1.8
 630  IF CL$(L) = "THIEF" THEN WU(L) = 1.5
 640  IF CL$(L) = "MONK" THEN WU(L) = 1.3
 650  IF CL$(L) = "RANGER" THEN WU(L) = 1.3
 660  IF CL$(L) = "PALADIN" THEN WU(L) = 1.1
 670  NEXT L
 699  RETURN

 700  REM SAVE
 705  PRINT : PRINT : PRINT : PRINT CHR$(4);"OPEN D:";NA$
 710  PRINT CHR$(4);"WRITE D:";NA$
 715  FOR L = 0 TO 3
 720  PRINT NM$(L)
 725  PRINT RA$(L)
 730  PRINT CL$(L)
 735  PRINT ZA%(L)
 740  PRINT AB%(L)
 745  PRINT AC%(L)
 750  PRINT AF%(L)
 755  PRINT AG%(L)
 760  PRINT AH%(L)
 765  PRINT AI%(L)
 770  PRINT AJ%(L)
 775  PRINT AK%(L)
 780  PRINT DV$(L)
 785  PRINT DW$(L)
 790  PRINT DX$(L)
 795  PRINT WT%(L)
 800  FOR M = 0 TO 7: PRINT AP$(L,M): NEXT
 805  FOR M = 0 TO 15: PRINT AX$(L,M): NEXT
 810  NEXT L
 815  PRINT X
 820  PRINT Y
 825  PRINT AD%: PRINT AE: PRINT FL%: PRINT RM$
 826  FOR I = 0 TO 5: PRINT FG%(I): NEXT I
 830  PRINT CHR$(4);"CLOSE D:";NA$
 840  PRINT CHR$(4);"BSAVE MAP:";NA$;",A$8000,L3200"
 850  RETURN

 900  REM LOAD LIST
 905  PRINT : PRINT : PRINT : PRINT CHR$(4);"OPEN PLIST"
 910  PRINT CHR$(4);"READ PLIST"
 925  FOR I = 0 TO 9
 930  INPUT PN$(I)
 935  NEXT I
 940  PRINT CHR$(4);"CLOSE PLIST"
 949  RETURN

 950  REM SAVE LIST
 955  PRINT : PRINT : PRINT : PRINT CHR$(4);"OPEN PLIST"
 960  PRINT CHR$(4);"WRITE PLIST"
 975  FOR I = 0 TO 9
 980  PRINT PN$(I)
 985  NEXT I
 990  PRINT CHR$(4);"CLOSE PLIST"
 999  RETURN

 1000  REM CREATE PARTY
 1010  D = 3 : GOSUB 9000
 1020  GOSUB 900
 1030  I = 0
 1040  IF PN$(I) = "" THEN 1100
 1050  I = I + 1 : IF I = 10 THEN 1080
 1060  GOTO 1040
 1080  HOME: PRINT "THERE IS NO ROOM": GET A$: GOTO 100

 1100  X = 45 : Y = 18 : RM$="ARRINEA": AE = 100: AD% = 100: FL% = 0
 1101  FOR I = 0 TO 5: FG%(I) = 0: NEXT I
 1110  FOR L = 0 TO 3
 1120  HOME
 1121 ZA%(L) = 0
 1122 AP$(L,1) = "DAGGER":AP$(L,2) = "LEATHER"
 1123 CL$(L) = "":RA$(L) = ""
 1124 DV$(L) = "LEATHER":DW$(L) = "DAGGER"
 1130  PRINT "THIS IS CHARACTER #";L+1
 1140  PRINT "WHAT SHOULD THIS CHARACTERS NAME BE?"
 1150  GOSUB 7000: NM$(L) = A$
 1160  IF A$ = "" THEN 1150
 1170  GOSUB 200: GOSUB 250: GOSUB 360
 1200  IF RA$(L) = "ELF" THEN AG%(L) = AG%(L) + 5
 1210  IF RA$(L) = "DWARF" THEN AJ%(L) = AJ%(L) + 5
 1220  IF RA$(L) = "HOBBIT" THEN AI%(L) = AI%(L) + 5
 1230  IF RA$(L) = "HUMAN" THEN AG%(L) = AG%(L) + 3
 1240  IF RA$(L) = "HUMAN" THEN AK%(L) = AK%(L) + 2
 1250  IF CL$(L) = "MONK" THEN AI%(L) = AI%(L) + 5
 1260  IF CL$(L) = "RANGER" THEN AB%(L) = 80
 1270  IF CL$(L) = "WIZARD" THEN AB%(L) = 50
 1280  IF CL$(L) = "CLERIC" THEN AB%(L) = 60
 1290  IF CL$(L) = "FIGHTER" THEN AB%(L) = 100
 1300  IF CL$(L) = "PALADIN" THEN AB%(L) = 80
 1310  IF CL$(L) = "THIEF" THEN AB%(L) = 60
 1320  IF CL$(L) = "MONK" THEN AB%(L) = 80
 1330 AB%(L) = AB%(L) + AJ%(L)
 1340 AB%(L) = AB%(L) * 2
 1350 AC%(L) = AB%(L)
 1360  IF CL$(L) = "FIGHTER" THEN WT%(L) = 20
 1370  IF CL$(L) = "WIZARD" THEN WT%(L) = 25
 1380  IF CL$(L) = "CLERIC" THEN WT%(L) = 20
 1390  IF CL$(L) = "THIEF" THEN WT%(L) = 20
 1400  IF CL$(L) = "PALADIN" THEN WT%(L) = 10
 1410  IF CL$(L) = "MONK" THEN WT%(L) = 10
 1420  IF CL$(L) = "RANGER" THEN WT%(L) = 10
 1430  NEXT L

 1500  HOME : REM REVIEWING WHOLE PARTY
 1501  FOR L = 0 TO 3
 1502  HOME : PRINT "REVIEWING CHARACTER ";L+1
 1503  PRINT "NAME: ";NM$(L)
 1504  PRINT "CLASS: ";CL$(L)
 1505  PRINT "RACE: ";RA$(L)
 1506  PRINT "STRENGTH: ";AF%(L)
 1507  PRINT "INTELLIGENCE: ";AG%(L)
 1508  PRINT "WISDOM: ";AH%(L)
 1509  PRINT "AGILITY: ";AI%(L)
 1510  PRINT "STAMINA: ";AJ%(L)
 1511  PRINT "CHARISMA: ";AK%(L)
 1512  PRINT "MAXIMUM LEVEL: ";WT%(L)
 1513  PRINT "HIT POINTS: ";AB%(L)
 1514  GET A$
 1515  NEXT L
 1516  HOME : PRINT "SAVE THIS PARTY (Y/N) ";: GET A$
 1520  IF A$<>"Y" AND A$<>"N" THEN 1516
 1530  IF A$ = "N" THEN 100

 1600  HOME : PRINT "ENTER YOUR PARTY'S NAME-": GOSUB 7000
 1620  NA$ = A$
 1630  IF NA$ = "" THEN 1600
 1640  I = 0
 1650  IF NA$ = PN$(I) GOTO 1680
 1660  I = I + 1 : IF I = 10 THEN 1700
 1670  GOTO 1650
 1680  PRINT:PRINT NA$;" ALREADY USED";: GET A$: GOTO 1600

 1700  REM SAVE ALL DATA
 1710  D = 3: GOSUB 9000: PRINT : PRINT  CHR$ (4);"BLOAD ARRINEA,A$8000"
 1740  I = 0
 1750  IF PN$(I) = "" THEN 1770
 1760  I = I + 1 : GOTO 1750
 1770  PN$(I) = NA$
 1780  GOSUB 700 : REM SAVE PARTY DATA
 1790  GOSUB 950 : REM SAVE PARTY LIST
 1799  GOTO 100


 2000  REM LIST
 2010  D = 3 : GOSUB 9000 : GOSUB 900
 2020  HOME: VTAB 1: HTAB 1: PRINT "NAME:"
 2030  FOR I = 1 TO 10
 2040  VTAB I * 2 + 1
 2050  HTAB 1
 2060  IF PN$(I - 1) = "" THEN PN$(I - 1) = "EMPTY"
 2070  PRINT PN$(I - 1)
 2080  NEXT I
 2090  PRINT : GET A$ : GOTO 100

 3000  REM RENAME
 3001  HOME : PRINT "NAME OF PARTY TO RENAME-": GOSUB 7000
 3002  IF A$ = "" THEN 100
 3003 NA$ = A$
 3004  HOME : PRINT "NEW NAME-": GOSUB 7000
 3005 B$ = A$
 3006  IF B$ = "" THEN 100
 3010  D = 3 : GOSUB 9000 : GOSUB 10000
 3020  PRINT : PRINT : PRINT : PRINT  CHR$ (4);"RENAME D:";NA$;",D:";B$
 3030  PRINT  CHR$ (4);"RENAME MAP:";NA$;",MAP:";B$
 3050  GOSUB 900 : REM LOAD PARTY LIST
 3060  I = 0
 3070  IF PN$(I) = NA$ THEN 3090
 3080  I = I + 1: GOTO 3070
 3090  PN$(I) = B$
 3095  GOSUB 950 : REM SAVE PARTY LIST
 3099  GOTO 100

 4000  REM DELETE
 4010  HOME : PRINT "NAME OF PARTY TO DELETE-": GOSUB 7000
 4020  IF A$ = "" THEN 100
 4030  NA$ = A$
 4040  D = 3 : GOSUB 9000 : GOSUB 10000
 4050  PRINT "PLEASE DO NOT DELETE A DEPLOYED PARTY.": PRINT "CONTINUE ?  (Y TO PROCEED)- ": GET A$
 4060  IF A$ <> "Y" THEN 100
 4070  PRINT : PRINT : PRINT : PRINT  CHR$ (4);"DELETE D:";NA$
 4072  PRINT  CHR$ (4);"DELETE MAP:";NA$
 4080  GOSUB 900 : REM LOAD PARTY LIST
 4090  I = 0
 4100  IF PN$(I) = NA$ THEN 4120
 4110  I = I + 1: GOTO 4100
 4120  PN$(I) = ""
 4130  GOSUB 950 : REM SAVE PARTY LIST
 4140  GOTO 100

 5000  REM DRIVE MAPPINGS
 5010  HOME
 5020  PRINT TAB(10);"SETUP DRIVE MAPPINGS"
 5030  PRINT : PRINT: PRINT
 5040  PRINT "DISK NAME  SLOT   DRIVE  VOLUME"
 5050  PRINT "-------------------------------"
 5060  FOR I = 0 TO 3
 5070  A$ = DL$(I) + "          ": A$ = LEFT$(A$,11)
 5080  PRINT A$;WS%(I);"      ";WD%(I);"      ";WV%(I)
 5090  NEXT I
 5100  PRINT "ENTER DISK NAME, 'FINISH', OR 'CANCEL' -": GOSUB 7000: PRINT
 5101  IF A$=DL$(0) OR A$=DL$(1) OR A$=DL$(2) OR A$=DL$(3) OR A$="FINISH" OR A$="CANCEL" THEN 5110
 5102  INPUT "RESPONSE NOT VALID. PRESS ENTER.";A$
 5103  GOTO 5000
 5110  IF A$="FINISH" THEN 5170
 5111  IF A$="CANCEL" THEN 100
 5120  IF A$=DL$(0) THEN I=0
 5121  IF A$=DL$(1) THEN I=1
 5122  IF A$=DL$(2) THEN I=2
 5123  IF A$=DL$(3) THEN I=3
 5130  INPUT "ENTER SLOT (1-7): ";B$
 5131  WS%(I) = VAL(B$): IF WS%(I)<1 OR WS%(I)>7 THEN 5130
 5140  INPUT "ENTER DRIVE (1-7): ";B$
 5141  WD%(I) = VAL(B$): IF WD%(I)<1 OR WD%(I)>9 THEN 5140
 5150  INPUT "ENTER VOLUME (1-255, USUALLY 254): ";B$
 5151  WV%(I) = VAL(B$): IF WV%(I)<1 OR WV%(I)>255 THEN 5150
 5160  GOTO 5000
 5170  D = 0: GOSUB 9000: GOSUB 5200
 5180  D = 3: GOSUB 9000: GOSUB 5200
 5190  GOTO 100

 5200  REM WRITE DRIVES
 5210  PRINT : PRINT : PRINT : PRINT CHR$(4);"OPEN DD
 5220  PRINT CHR$(4);"WRITE DD
 5230  FOR I = 0 TO 3
 5240  PRINT DL$(I)
 5250  PRINT WS%(I)
 5260  PRINT WD%(I)
 5270  PRINT WV%(I)
 5280  NEXT I
 5290  PRINT CHR$(4);"CLOSE DD
 5299  RETURN

 5300  REM READ DRIVES
 5310  PRINT : PRINT : PRINT : PRINT CHR$(4);"OPEN DD"
 5320  PRINT CHR$(4);"READ DD"
 5330  FOR I = 0 TO 3
 5340  INPUT DL$(I)
 5350  INPUT WS%(I)
 5360  INPUT WD%(I)
 5370  INPUT WV%(I)
 5380  NEXT I
 5390  PRINT CHR$(4);"CLOSE DD"
 5399  RETURN

 6000  REM DEPLOY PARTY
 6010  D = 3 : GOSUB 400 : GOSUB 500
 6020  D = 0 : GOSUB 9000
 6030  ONERR GOTO 6100
 6040  PRINT:PRINT:PRINT:PRINT CHR$(4);"VERIFY PLIST"
 6050  POKE 216,0: CALL -3288: GOSUB 900: GOTO 6200
 6100  POKE 216,0: CALL -3288: PN$(0) = NA$ : GOSUB 950: GOSUB 700
 6110  HOME: PRINT "TO PLAY ";NA$: PRINT "BOOT FROM MASTER.";: GET A$: GOTO 100

 6200  REM RECALL PARTY
 6210  PRINT: PRINT "RECALLING ";PN$(0)
 6220  SV$ = NA$: NA$ = PN$(0): GOSUB 500
 6230  PRINT: PRINT CHR$(4);"DELETE D:";NA$
 6340  PRINT CHR$(4);"DELETE MAP:";NA$
 6350  D = 3: GOSUB 9000: GOSUB 700
 6360  NA$ = SV$: GOSUB 500
 6370  D = 0: GOSUB 9000: GOTO 6100

 7000  REM INPUT
 7010 A$ = "": HTAB 1: PRINT ">";
 7020  GET B$
 7030  IF B$ =  CHR$ (13) THEN  RETURN
 7040  IF B$ =  CHR$ (8) THEN 7080
 7050  IF B$ =  CHR$ (10) OR B$ =  CHR$ (11) OR B$ =  CHR$ (21) THEN 7020
 7060  IF  LEN (A$) > 16 THEN 7020
 7070  PRINT B$;:A$ = A$ + B$: GOTO 7020
 7080  IF  LEN (A$) < 2 THEN 7000
 7090 A$ =  LEFT$ (A$, LEN (A$) - 1): POKE 36, PEEK (36) - 1: CALL  - 868: GOTO 7010

 8000  REM GLOBALS
 8010  DIM PN$(9): DIM A%(5)
 8020  DIM WS%(3): DIM WD%(3): DIM WV%(3): REM SLOT DRIVE VOLUME
 8030  DIM DL$(3): REM DISK LABELS
 8040  DIM WU(3): DIM FG%(5)
 8050  DIM NM$(3): DIM RA$(3)
 8060  DIM CL$(3): DIM ZA%(3)
 8070  DIM AB%(3): DIM AC%(3)
 8080  DIM AF%(3): DIM AG%(3)
 8090  DIM AH%(3): DIM AI%(3)
 8100  DIM AJ%(3): DIM AK%(3)
 8110  DIM DV$(3): DIM DW$(3)
 8120  DIM DX$(3): DIM WT%(3)
 8130  DIM GN(9): DIM PM%(9)
 8140  DIM PC%(3): DIM GP%(3)
 8150  DIM AX$(3,15): DIM AP$(3,7)
 8190  REM POKE IN STACK FIXER
 8191  POKE 768,104 : POKE 769,168 : POKE 770,104 : POKE 771,166 : POKE 772,223 : POKE 773,154 : POKE 774,72 : POKE 775,152 : POKE 776,72 : POKE 777,96
 8199  RETURN

 9000  REM VERIFY DISK
 9110  ONERR  GOTO 9200
 9120  PRINT : PRINT : PRINT
 9130  PRINT  CHR$(4);"VERIFY DISK";D;",S";WS%(D);",D";WD%(D);",V";WV%(D)
 9140  POKE 216,0 : RETURN
 9200  HOME : PRINT "INSERT ";DL$(D);" DISK IN D";WD%(D);"- ";: GET A$
 9210  CALL 768
 9220  PRINT : PRINT : PRINT : RESUME

 10000  REM VERIFY PARTY
 10110  ONERR  GOTO 10200
 10120  PRINT : PRINT : PRINT
 10130  PRINT  CHR$(4);"VERIFY MAP:";NA$
 10140  RETURN
 10200  HOME : PRINT "'";NA$;"' NOT ON THIS DISK. ";: GET A$
 10210  CALL -3288: REM CLEAR THE STACK
 10220  POKE 216,0 : GOTO 100

 12000  HOME : PRINT "NO ROOM ON THIS DISK. ";: GET A$: GOTO 100
