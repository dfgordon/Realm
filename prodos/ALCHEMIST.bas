 3720  TEXT: HOME: PRINT "PLACING: ";B$: GOSUB 4210
 3730  PRINT: PRINT "USE ARROWS TO CHANGE CHARACTER": PRINT "1-8 REPLACES, ESC ABORTS ": GET A$
 3740  IF A$ = CHR$(27) THEN PRINT: PRINT "SALE ABORTED! ";: GET A$: RETURN
 3741  IF A$ = CHR$(8) THEN A = A - 1: GOTO 3790
 3742  IF A$ = CHR$(21) THEN A = A + 1: GOTO 3790
 3743  I = VAL(A$) - 1: IF I<0 OR I>7 THEN 3790
 3750  IF AP$(A,I) = "" THEN 3760
 3751  HOME: PRINT "REPLACE ";AP$(A,I);" (Y/N) ? ";: GET A$: IF A$ <> "Y" AND A$ <> "N" THEN 3751
 3752  IF A$ = "N" THEN 3720
 3760 AP$(A,I) =  LEFT$ (B$ + " ", LEN (B$)): AD% = AD% - C: GOSUB 7100: PRINT: PRINT "SALE COMPLETE! ";: GET A$: RETURN
 3790  IF A<0 THEN A = 0
 3791  IF A>3 THEN A = 3
 3792  GOTO 3720

 4210  PRINT NM$(A);"'S ITEMS:": FOR I = 0 TO 7: PRINT I + 1;") ";: PRINT AP$(A,I): NEXT: RETURN

 4300  PRINT "FOR SALE:"
 4330  FOR I = 1 TO 4
 4340  ON I GOSUB 20000,20010,20020,20030
 4345  GOSUB 19000
 4350  PRINT I;") ";B$;" (";C;")"
 4360  NEXT I
 4370  RETURN

 7100 I = 0
 7105  FOR P = 0 TO 7: IF DW$(A) = AP$(A,P) THEN I = 8
 7106  NEXT : IF I = 0 THEN DW$(A) = "HA"+"NDS"
 7109 I = 0
 7110  FOR P = 0 TO 7: IF DV$(A) = AP$(A,P) THEN I = 8
 7111  NEXT : IF I = 0 THEN DV$(A) = "SK"+"IN"
 7115  RETURN

 7750  PRINT: PRINT "-HEALING THE PARTY-": GOSUB 30000
 7752  FOR I = 0 TO 3: IF AB%(I)>0 THEN AB%(I) = AC%(I)
 7753  NEXT
 7754  PRINT "PARTY IS HEALED! ";: GET A$: GOTO 9510

 7760  M = 1: FOR I = 0 TO 3: M = M * AB%(I): NEXT I
 7761  IF M > 0 THEN PRINT: AD% = AD% + C: PRINT "NO ONE IS DEAD! ";: GOSUB 30050: GOTO 9510
 7765  PRINT: PRINT "-RESURRECTING-": GOSUB 30000
 7766  FOR I = 0 TO 3
 7767  IF AB%(I) = 0 THEN AB%(I) = AC%(I): PRINT NM$(I);" IS RAISED!"
 7768  NEXT I
 7769  GET A$: GOTO 9510

 8900 REM

 9500  PRINT "CHARACTER TO TRANSACT (#) ";: GET A$:A =  VAL (A$) - 1: IF A < 0 OR A > 3 THEN HOME: VTAB 21: GOTO 9500
 9501  IF AB%(A) = 0 THEN HOME: VTAB 21: GOTO 9500
 9510  TEXT: HOME: PRINT "WELCOME TO THE ALCHEMIST:"
 9520  GOSUB 4300
 9530  PRINT "GOLD: ";AD%;"- BUY (#) OR LEAVE (ESC): ";: GET A$: IF A$ = CHR$(27) THEN 25000
 9540  I = VAL(A$): IF I<1 OR I>4 THEN 9510
 9550  ON I GOSUB 20000,20010,20020,20030

 9560  GOSUB 19000: IF AD% < C THEN PRINT: PRINT "YOU DON'T HAVE THE GOLD! ";: GOSUB 30050: GOTO 9510
 9561  AD% = AD% - C
 9562  IF B$ = "HEAL" THEN 7750
 9563  IF B$ = "RESURRECT" THEN 7760
 9570  AD% = AD% + C: L = A: GOSUB 3720: A = L: GOTO 9510

 19000 C = INT (C * (1 - AK%(A) / 100)): RETURN

 20000 C = 50: B$ = "HEAL": RETURN
 20010 C = 500: B$ = "RESURRECT": RETURN
 20020 C = 200: B$ = "POTION OF PASSAGE": RETURN
 20030 C = 4000: B$ = "FLYING CARPET": RETURN

 25000  POKE -16304,0: POKE 16384,0: POKE 103,1: POKE 104,64: GOTO 25000

 30000  POKE 769,30: POKE 813,100: POKE 768,169: CALL 768: RETURN
 30050  POKE 8191,50: CALL 912
 30055  GET A$: RETURN
