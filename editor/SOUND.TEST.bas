1 DIM US(7): DIM PA(5): PT$ = "/PRODOS322MG/REALM/"
2 US(0) = 778: REM TONE
3 US(1) = 792: REM FAT TONE
4 US(2) = 806: REM UP CHIRP
5 US(3) = 829: REM DOWN CHIRP
6 US(4) = 852: REM UP FAT CHIRP
7 US(5) = 875: REM DOWN FAT CHIRP
8 US(6) = 898: REM SIREN
9 US(7) = 923: REM FAT SIREN

10 PRINT CHR$(4);"BLOAD ";PT$;"BIN/SOUND"
20 SE = 1: PA(1) = 100: PA(2) = 50: PA(3) = 5: PA(4) = 3: PA(5) = 1
30 HOME: PRINT "REALM SOUND TESTER"
100 GOSUB 2000: GET A$
110 IF A$ = "Q" THEN END
140 IF A$ = CHR$(8) OR A$ = CHR$(11) THEN SE = SE - 1
150 IF A$ = CHR$(10) OR A$ = CHR$(21) THEN SE = SE + 1
160 IF SE<1 THEN SE = 13
170 IF SE>13 THEN SE = 1
180 IF A$ = CHR$(13) THEN 200
190 GOTO 100

200 IF SEL<6 THEN 300
210 POKE 6,PA(1): POKE 7,PA(2): POKE 8,PA(3): POKE 9,PA(4): POKE 30,PA(5): CALL US(SEL-6): GOTO 100

300 HTAB 1: VTAB 20: INPUT "ENTER VALUE (1-255): ";PA(SEL)
310 IF (PA(2)-PA(1))/PA(5) <> INT((PA(2)-PA(1))/PA(5)) THEN PRINT "RATE MUST DIVIDE EVENLY.";: GET A$: GOTO 300
320 IF PA(1)>PA(2) GOTO 30
330 PRINT "REQUIRE LOW PERIOD > HIGH PERIOD";: GET A$: GOTO 300

2000 REM DISPLAY INTERFACE
2005 RESTORE
2010 FOR I = 1 TO 13
2020 READ A$: IF SE = I THEN INVERSE
2030 HTAB 1: VTAB I+3: PRINT A$;: NORMAL
2040 IF I<6 THEN PRINT ": ";PA(I);TAB(38)
2050 IF I>5 THEN PRINT
2060 NEXT
2070 RETURN

10000 DATA "LOW TONE PERIOD","HIGH TONE PERIOD","CYCLES PER TONE","REPS","CHIRP RATE"
10010 DATA "TONE","FAT TONE"
10020 DATA "UPCHIRP","DOWNCHIRP","FAT UPCHIRP","FAT DOWNCHIRP"
10030 DATA "SIREN","FAT SIREN"
