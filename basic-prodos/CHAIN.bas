 10 M = FRE(0)
 20 IF PG$="TOWN" THEN 100
 22 IF PG$="OUTSIDE" THEN 300
 24 IF PG$="DUNGEON" THEN 500
 25 IF PG$="COMBAT" THEN 600

 100  REM
 105  GOSUB 9000
 110  IF R%=1 THEN 160
 115  IF MD%=1 THEN PRINT CHR$(4);"BSAVE PARTIES/";LEFT$(RM$,2);".";NA$;",A$6E00,L3200": MD% = 0
 120  IF RM$ = "ARRINEA" THEN PRINT CHR$(4);"BLOAD MAPS/TOWNPIC.";X;".";Y;",A$6E00"
 121  IF RM$ <> "ARRINEA" THEN PRINT CHR$(4);"BLOAD XMAPS/TOWNPIC.";X;".";Y;",A$6E00"
 130  I = 28960 : GOSUB 8000
 140  PRINT CHR$(4);"BLOAD BIN/TWNSPS"
 160  PRINT CHR$(4);"BLOAD ";RD$;PG$
 170  IF R%=1 THEN 190
 180  POKE 16384,0: POKE 103,1: POKE 104,64: GOTO 0
 190  POKE 16384,0: POKE 103,1: POKE 104,64: GOTO 10

 300  REM
 305  GOSUB 9000
 310  PRINT CHR$(4);"BLOAD PARTIES/";LEFT$(RM$,2);".";NA$
 330  PRINT CHR$(4);"BLOAD ";RD$;PG$
 340  POKE 16384,0: POKE 103,1: POKE 104,64: GOTO 5

 500  IF R%=1 THEN 580
 501  IF R%=2 OR MD%=0 THEN 530
 520  GOSUB 9000: PRINT CHR$(4);"BSAVE PARTIES/";LEFT$(RM$,2);".";NA$;",A$6E00,L3200": MD% = 0
 530  PRINT CHR$(4);"BLOAD XMAPS/DNGPIC.";X;".";Y;",A$6E00"
 540  I = 30160 : GOSUB 8000
 560  PRINT CHR$(4);"BLOAD BIN/DNGNSPS"
 580  PRINT CHR$(4);"BLOAD ";RD$;PG$
 581  IF R% = 1 THEN PRINT CHR$(4);"BLOAD ";RD$;"TEMP"
 585  IF R%=1 THEN 595
 590  POKE 16384,0: POKE 103,1: POKE 104,64: GOTO 0
 595  POKE 16384,0: POKE 103,1: POKE 104,64: GOTO 10

 600  REM
 620  GOSUB 9000: PRINT CHR$(4);"BLOAD ";RD$;PG$
 630  POKE 16384,0: POKE 103,1: POKE 104,64: GOTO 10

 8000  B$ = ""
 8010  IF  PEEK (I) = 0 THEN 8030
 8020  B$ = B$ +  CHR$ ( PEEK (I)):I = I + 1: GOTO 8010
 8030  A = (40 -  LEN (B$)) / 2: HOME : VTAB 21
 8040  PRINT  TAB( A);B$: FOR P = 0 TO EP: NEXT P
 8050  RETURN

 9000  REM VERIFY DISK
 9110  ONERR  GOTO 9200
 9120  PRINT: PRINT  CHR$(4);"VERIFY START.REALM": POKE 216,0: RETURN
 9200  HOME : VTAB 21: PRINT "PLEASE RESTORE DISK- ";: GET A$: CALL 768: PRINT : RESUME
