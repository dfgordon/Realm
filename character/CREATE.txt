 1  CLEAR
 2  GOSUB 8000 : X = 51 : Y = 33 : RM$="ARRINEA"
 10  TEXT : HOME
 169  FOR L = 0 TO 3
 170  HOME
 171 ZA%(L) = 0:AD%(L) = 250:AE = 100
 172 AP$(L,1) = "DAGGER":AP$(L,2) = "LEATHER"
 173 CL$(L) = "":RA$(L) = ""
 174 DV$(L) = "LEATHER":DW$(L) = "DAGGER"
 180  PRINT "THIS IS CHARACTER #";L+1
 190  PRINT "WHAT SHOULD THIS CHARACTERS NAME BE?"
 191  GOSUB 7000
 192 NM$(L) = A$
 193  IF A$ = "" THEN 170
 195  GOSUB 345 : REM SET ABILITIES

 200  HOME
 210  PRINT "WHAT CLASS SHOULD ";NM$(L);" BE?"
 220  PRINT "1) FIGHTER"
 221  PRINT "2) WIZARD"
 222  PRINT "3) CLERIC"
 223  PRINT "4) THIEF"
 224  PRINT "5) MONK"
 225  PRINT "6) PALADIN"
 226  PRINT "7) RANGER"
 230  GET A$
 240  IF A$ = "1" THEN CL$(L) = "FIGHTER"
 241  IF A$ = "2" THEN CL$(L) = "WIZARD"
 242  IF A$ = "3" THEN CL$(L) = "CLERIC"
 243  IF A$ = "4" THEN CL$(L) = "THEIF"
 244  IF A$ = "5" THEN CL$(L) = "MONK"
 245  IF A$ = "6" THEN CL$(L) = "PALADIN"
 246  IF A$ = "7" THEN CL$(L) = "RANGER"
 247  IF CL$(L) = "" THEN 200

 250  HOME
 260  PRINT "WHAT RACE SHOULD ";NM$(L);" BE?"
 270  PRINT "1) HUMAN"
 271  PRINT "2) ELF"
 272  PRINT "3) DWARF"
 273  PRINT "4) HOBBIT"
 280  GET A$
 290  IF A$ = "1" THEN RA$(L) = "HUMAN"
 300  IF A$ = "2" THEN RA$(L) = "ELF"
 310  IF A$ = "3" THEN RA$(L) = "DWARF"
 320  IF A$ = "4" THEN RA$(L) = "HOBBIT"
 321  IF RA$(L) = "" THEN 250
 330  IF A$ = "2" AND CL$(L) = "CLERIC" THEN 250
 331  IF A$ = "2" AND CL$(L) = "PALADIN" THEN 250
 332  IF A$ = "2" AND CL$(L) = "THEIF" THEN 250
 333  IF A$ = "3" AND CL$(L) = "MONK" THEN 250
 334  IF A$ = "3" AND CL$(L) = "WIZARD" THEN 250
 335  IF A$ = "3" AND CL$(L) = "PALADIN" THEN 250
 336  IF A$ = "3" AND CL$(L) = "CLERIC" THEN 250
 337  IF A$ = "4" AND CL$(L) = "MONK" THEN 250
 338  IF A$ = "4" AND CL$(L) = "PALADIN" THEN 250
 339  IF A$ = "4" AND CL$(L) = "WIZARD" THEN 250
 340  IF A$ = "4" AND CL$(L) = "RANGER" THEN 250
 341  GOTO 700

 345  HOME: REM ABILITIES SUBROUTINE
 350  PRINT "NOW CHOOSE YOUR ABILITIES:"
 370 ZA = 75
 380  PRINT "WHAT STRENGTH:"
 381  GOSUB 7000:Z =  VAL (A$)
 382  IF Z > 20 OR Z < 5 THEN 345
 383 AF%(L) = Z
 390 ZA = ZA - AF%(L)
 400  PRINT " ";ZA;" POINTS LEFT"
 405  IF ZA < 0 THEN 345
 410  PRINT "WHAT INTELLIGENCE:"
 411  GOSUB 7000:Z =  VAL (A$)
 412  IF Z > 20 OR Z < 5 THEN 345
 413 AG%(L) = Z
 415 ZA = ZA - AG%(L)
 418  PRINT " ";ZA;" POINTS LEFT"
 420  IF ZA < 0 THEN 345
 421  PRINT "WHAT WISDOM:"
 422  GOSUB 7000:Z =  VAL (A$)
 423  IF Z > 20 OR Z < 5 THEN 345
 424 AH%(L) = Z:ZA = ZA - AH%(L)
 425  PRINT " ";ZA;" POINTS LEFT"
 426  IF ZA < 0 THEN 345
 428  PRINT "WHAT AGILITY:"
 429  GOSUB 7000:Z =  VAL (A$)
 430  IF Z > 20 OR Z < 5 THEN 345
 431 AI%(L) = Z:ZA = ZA - AI%(L)
 432  PRINT " ";ZA;" POINTS LEFT"
 434  IF ZA < 0 THEN 345
 436  PRINT "WHAT STAMINA:"
 437  GOSUB 7000:Z =  VAL (A$)
 438  IF Z > 20 OR Z < 5 THEN 345
 439 AJ%(L) = Z:ZA = ZA - AJ%(L)
 440  PRINT " ";ZA;" POINTS LEFT"
 442  IF ZA < 0 THEN 345
 444  IF ZA > 20 OR ZA < 5 THEN 345
 445  PRINT "CHARISMA= ";ZA
 450 AK%(L) = ZA
 452  FOR P = 1 TO 900: NEXT P
 453  RETURN

 700  HOME : PRINT "SATISFACTORY (Y OR N) ";: GET A$
 701  IF A$ = "N" THEN 170
 702  IF RA$(L) = "ELF" THEN AI%(L) = AI%(L) + 5
 703  IF RA$(L) = "DWARF" THEN AJ%(L) = AJ%(L) + 5
 704  IF RA$(L) = "HOBBIT" THEN AI%(L) = AI%(L) + 5
 705  IF RA$(L) = "HUMAN" THEN AG%(L) = AG%(L) + 5
 706  IF RA$(L) = "HUMAN" THEN AK%(L) = AK%(L) + 5
 707  IF CL$(L) = "MONK" THEN AI%(L) = AI%(L) + 5
 710  IF CL$(L) = "RANGER" THEN AB%(L) = 80
 711  IF CL$(L) = "WIZARD" THEN AB%(L) = 50
 712  IF CL$(L) = "CLERIC" THEN AB%(L) = 60
 713  IF CL$(L) = "FIGHTER" THEN AB%(L) = 100
 714  IF CL$(L) = "PALADIN" THEN AB%(L) = 80
 715  IF CL$(L) = "THEIF" THEN AB%(L) = 60
 716  IF CL$(L) = "MONK" THEN AB%(L) = 80
 720 AB%(L) = AB%(L) + AJ%(L)
 721 AB%(L) = AB%(L) * 2
 725 AC%(L) = AB%(L)
 729  IF CL$(L) = "FIGHTER" THEN WT%(L) = 20
 730  IF CL$(L) = "WIZARD" THEN WT%(L) = 25
 731  IF CL$(L) = "CLERIC" THEN WT%(L) = 20
 732  IF CL$(L) = "THEIF" THEN WT%(L) = 20
 733  IF CL$(L) = "PALADIN" THEN WT%(L) = 10
 734  IF CL$(L) = "MONK" THEN WT%(L) = 10
 735  IF CL$(L) = "RANGER" THEN WT%(L) = 10
 738  NEXT L

 739  HOME : REM REVIEWING WHOLE PARTY
 740  FOR L = 0 TO 3
 741  HOME : PRINT "REVIEWING CHARACTER ";L+1
 750  PRINT "NAME: ";NM$(L)
 760  PRINT "CLASS: ";CL$(L)
 770  PRINT "RACE: ";RA$(L)
 780  PRINT "STRENGTH: ";AF%(L)
 790  PRINT "INTELLIGENCE: ";AG%(L)
 800  PRINT "WISDOM: ";AH%(L)
 810  PRINT "AGILITY: ";AI%(L)
 820  PRINT "STAMINA: ";AJ%(L)
 830  PRINT "CHARISMA: ";AK%(L)
 831  PRINT "MAXIMUM LEVEL: ";WT%(L)
 832  PRINT "HIT POINTS: ";AB%(L)
 835  GET A$
 840  NEXT L
 850  HOME : PRINT "SATISFACTORY (Y OR N) ";: GET A$
 860  IF A$ = "N" THEN  GOTO 1
 865  GOSUB 9000 : GOSUB 1900 : REM LOAD PARTY LIST
 870  HOME : PRINT "ENTER YOUR PARTY'S NAME-": GOSUB 7000
 880  NA$ = A$
 890  IF NA$ = "" THEN 870
 892  I = 0
 893  IF NA$ = PN$(I) GOTO 899
 894  I = I + 1 : IF I = 10 THEN 900
 895  GOTO 893
 899  PRINT:PRINT NA$;" ALREADY USED":PRINT "ENTER YOUR PARTY'S NAME-":GOSUB 7000:GOTO 880

 900  REM SAVE ALL DATA
 910  PRINT : PRINT : PRINT : PRINT  CHR$ (4);"BLOAD ARRINEA,A$8000"
 920  POKE 27888,0: POKE 27889,0: POKE 27890,0: POKE 27891,0: POKE 27892,0
 930  GOSUB 9000
 940  I = 0
 941  IF PN$(I) = "" THEN 950
 942  I = I + 1 : GOTO 941
 950  PN$(I) = NA$
 960  GOSUB 1700 : REM SAVE PARTY DATA
 970  GOSUB 1950 : REM SAVE PARTY LIST
 980  PRINT : PRINT CHR$(4);"RUN CHOOSE"

 1700  REM SAVE A PARTY
 1705  PRINT : PRINT : PRINT : PRINT CHR$(4);"OPEN D:";NA$
 1710  PRINT CHR$(4);"WRITE D:";NA$
 1715  FOR L = 0 TO 3
 1720  PRINT NM$(L)
 1725  PRINT RA$(L)
 1730  PRINT CL$(L)
 1735  PRINT ZA%(L)
 1740  PRINT AB%(L)
 1745  PRINT AC%(L)
 1750  PRINT AF%(L)
 1755  PRINT AG%(L)
 1760  PRINT AH%(L)
 1765  PRINT AI%(L)
 1770  PRINT AJ%(L)
 1775  PRINT AK%(L)
 1780  PRINT DV$(L)
 1785  PRINT DW$(L)
 1790  PRINT DX$(L)
 1795  PRINT WT%(L)
 1800  FOR M = 0 TO 7: PRINT AP$(L,M): NEXT
 1805  FOR M = 0 TO 15: PRINT AX$(L,M): NEXT
 1810  NEXT L
 1815  PRINT X
 1820  PRINT Y
 1825  PRINT AD%: PRINT AE: PRINT RM$
 1830  PRINT CHR$(4);"CLOSE D:";NA$
 1835  PRINT CHR$(4);"BSAVE CK:";NA$;",A$6CF0,L$5"
 1840  PRINT CHR$(4);"BSAVE MAP:";NA$;",A$8000,L3200"
 1850  RETURN

 1900  REM LOAD PARTY LIST
 1905  PRINT : PRINT : PRINT : PRINT CHR$(4);"OPEN PLIST"
 1910  PRINT CHR$(4);"READ PLIST"
 1925  FOR I = 0 TO 9
 1930  INPUT PN$(I)
 1935  NEXT I
 1940  PRINT CHR$(4);"CLOSE PLIST"
 1949  RETURN

 1950  REM SAVE PARTY LIST
 1955  PRINT : PRINT : PRINT : PRINT CHR$(4);"OPEN PLIST"
 1960  PRINT CHR$(4);"WRITE PLIST"
 1975  FOR I = 0 TO 9
 1980  PRINT PN$(I)
 1985  NEXT I
 1990  PRINT CHR$(4);"CLOSE PLIST"
 1999  RETURN

 7000  REM STRING ENTRY
 7010 A$ = "": HTAB 1: PRINT ">";
 7020  GET B$
 7030  IF B$ =  CHR$ (13) THEN  RETURN
 7040  IF B$ =  CHR$ (8) THEN 7080
 7050  IF B$ =  CHR$ (10) OR B$ =  CHR$ (11) OR B$ =  CHR$ (21) THEN 7020
 7060  IF  LEN (A$) > 16 THEN 7020
 7070  PRINT B$;:A$ = A$ + B$: GOTO 7020
 7080  IF  LEN (A$) < 2 THEN 7000
 7090 A$ =  LEFT$ (A$, LEN (A$) - 1): POKE 36, PEEK (36) - 1: CALL  - 868: GOTO 7010

 8000  REM DECLARE VARIABLES
 8010  DIM PN$(9)
 8020  DIM WS%(3): DIM WD%(3): DIM WV%(3): REM SLOT DRIVE VOLUME
 8030  DIM DL$(3): REM DISK LABELS
 8040  DIM WU(3): REM HIT MODIFIERS
 8050  DIM NM$(3): DIM RA$(3)
 8060  DIM CL$(3): DIM ZA%(3)
 8070  DIM AB%(3): DIM AC%(3)
 8080  DIM AF%(3): DIM AG%(3)
 8090  DIM AH%(3): DIM AI%(3)
 8100  DIM AJ%(3): DIM AK%(3)
 8110  DIM DV$(3): DIM DW$(3)
 8120  DIM DX$(3): DIM WT%(3)
 8130  DIM B$(3): DIM PM%(9)
 8140  DIM PC%(3): DIM GP%(3)
 8150  DIM AX$(3,15): DIM AP$(3,7)
 8190  REM POKE IN STACK FIXER
 8191  POKE 768,104 : POKE 769,168 : POKE 770,104 : POKE 771,166 : POKE 772,223 : POKE 773,154 : POKE 774,72 : POKE 775,152 : POKE 776,72 : POKE 777,96
 8199  RETURN

 9000  REM VERIFY DISK SUBROUTINE
 9110  ONERR  GOTO 9200
 9120  PRINT : PRINT : PRINT
 9130  PRINT  CHR$(4);"VERIFY DISK3"
 9140  RETURN
 9200  HOME : PRINT "PLEASE RESTORE CHARACTER DISK- ";: GET A$
 9210  CALL 768
 9220  PRINT : PRINT : PRINT : RESUME
