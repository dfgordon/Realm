 1 D$ =  CHR$ (4)
 10  PRINT D$;"BLOAD TITLE.PIC"
 60  HOME 
 70  POKE  - 16304,0: POKE  - 16302,0: GET A$
 80  TEXT : HOME 
 130  PRINT "SELECT ONE-"
 140  PRINT "1) GENERATE A PARTY
 150  PRINT "2) COPY A PARTY
 155  PRINT "3) LIST EXISTING PARTIES
 160  PRINT "4) RENAME A PARTY
 170  PRINT "5) DELETE A PARTY
 180  PRINT "6) SET DISK DRIVES
 185  PRINT "7) ENTER THE GAME
 190  GET A$
 200 A =  VAL (A$)
 210  ON A GOTO 1000,2000,3000,4000,5000,6000,250
 250  CLEAR : RESTORE 
 260  LOMEM: 27904
 265  HIMEM: 32767
 268  DIM TW$(8): DIM WU(3)
 270 TW$(0) = "GUARD":TW$(1) = "HUMAN":TW$(2) = "ELF"
 272 TW$(3) = "DWARF":TW$(4) = "HOBBIT":TW$(5) = "ADVENTURER"
 274 TW$(6) = "WIZARD":TW$(7) = "MARINE":TW$(8) = "DRAGON"
 275 P = 0
 280 D$ =  CHR$ (4)
 282  DIM NM$(3): DIM RA$(3)
 283  DIM CL$(3): DIM ZA%(3)
 284  DIM AB%(3): DIM AC%(3)
 285  DIM AF%(3): DIM AG%(3)
 286  DIM AH%(3): DIM AI%(3)
 287  DIM AJ%(3): DIM AK%(3)
 289  DIM DV$(3): DIM DW$(3)
 290  DIM DX$(3): DIM WT%(3)
 291  DIM B$(3): DIM PM%(9)
 292  DIM PC%(3): DIM GP%(3)
 295  DIM AX$(3,15): DIM AP$(3,7)
 299  POKE 27893,0: POKE 27894,0: POKE 27895,0
 300  HOME : PRINT "ENTER THE PARTY'S NAME-": GOSUB 7000
 301 NA$ = A$: IF NA$ = "" THEN 80
 302  HOME : PRINT "WHAT VOLUME IS PARTY ON? ": GOSUB 7000
 303  ONERR  GOTO 10000
 305  PRINT : PRINT : PRINT : PRINT D$;"VERIFY CK:";NA$;",V";A$
 306  PRINT D$;"BLOAD CK:";NA$;",A$6CF0
 310  PRINT D$;"OPEN D:";NA$
 320  PRINT D$;"READ D:";NA$
 330  FOR L = 0 TO 3
 340  INPUT NM$(L)
 350  INPUT RA$(L)
 360  INPUT CL$(L)
 370  INPUT ZA%(L)
 380  INPUT AB%(L)
 390  INPUT AC%(L)
 420  INPUT AF%(L)
 430  INPUT AG%(L)
 440  INPUT AH%(L)
 450  INPUT AI%(L)
 460  INPUT AJ%(L)
 470  INPUT AK%(L)
 480  INPUT DV$(L)
 490  INPUT DW$(L)
 500  INPUT DX$(L)
 501  INPUT WT%(L)
 510  FOR M = 0 TO 7: INPUT AP$(L,M): NEXT 
 520  FOR M = 0 TO 15: INPUT AX$(L,M): NEXT 
 530  NEXT L
 671  INPUT X
 672  INPUT Y
 675  INPUT AD%: INPUT AE
 680  PRINT D$;"CLOSE D:";NA$
 681  PRINT D$;"BLOAD MRDCK:";NA$;",A$8000"
 682  IF P = 1 THEN 2003
 685  HOME : REM  HOW MANY DRIVES USED TO BE HERE
 686 A = 1: REM  NUM DRIVES
 687  IF A > 2 THEN 685
 688  IF A < 1 THEN 685
 689 WV% = A: PRINT 
 690  ONERR  GOTO 695
 691  GOTO 696
 695  HOME : PRINT "INSERT MASTER DISK- ";: GET A$
 696  PRINT : PRINT : PRINT : PRINT D$;"BLOAD SDP.INTRP,A$1100,V30"
 697  FOR L = 0 TO 3:WU(L) = 1
 698  IF CL$(L) = "WIZARD" THEN WU(L) = 2
 699  IF CL$(L) = "CLERIC" THEN WU(L) = 1.8
 700  IF CL$(L) = "THIEF" THEN WU(L) = 1.5
 701  IF CL$(L) = "MONK" THEN WU(L) = 1.3
 702  IF CL$(L) = "RANGER" THEN WU(L) = 1.3
 703  IF CL$(L) = "PALADIN" THEN WU(L) = 1.1
 704  NEXT L
 705  FOR I = 0 TO 8:TW$(I) = TW$(I) +  CHR$ (0): NEXT 
 706 NA$ =  LEFT$ (NA$ + " ", LEN (NA$))
 709  PRINT D$;"BLOAD CHAIN"
 710  PRINT D$;"BLOAD MRDCK'S"
 711  PRINT "ADD HOW MUCH GOLD ";: INPUT A
 712 AD% = AD% + A
 720  POKE 103,0: POKE 104,64: GOTO 0
 1000  PRINT : PRINT : PRINT 
 1010  ONERR  GOTO 1020
 1015  GOTO 1030
 1020  HOME : PRINT "INSERT CHARACTER GENERATOR- ";: GET A$
 1030  PRINT : PRINT : PRINT : PRINT  CHR$ (4);"VERIFY CDISK,V33"
 1040  PRINT  CHR$ (4);"RUN CREATE"
 2000  CLEAR 
 2001 P = 1
 2002  GOTO 280
 2003  ONERR  GOTO 11000
 2005  HOME : PRINT "SAVE TO WHICH VOLUME? ": GOSUB 7000
 2006  PRINT : PRINT : PRINT : PRINT D$;"OPEN PLIST,V";A$
 2007  PRINT D$;"READ PLIST
 2008  INPUT N
 2009  FOR I = 1 TO N: INPUT A$(I - 1): NEXT 
 2010  PRINT D$;"CLOSE PLIST
 2011 P = 0
 2012  IF A$(P) = "" THEN 2015
 2013 P = P + 1: IF P = N THEN 12000
 2014  GOTO 2012
 2015 A$(P) = NA$
 2020  ONERR  GOTO 13000
 2025  PRINT D$;"OPEN D:";NA$
 2030  PRINT D$;"WRITE D:";NA$
 2035  FOR L = 0 TO 3
 2040  PRINT NM$(L)
 2045  PRINT RA$(L)
 2050  PRINT CL$(L)
 2055  PRINT ZA%(L)
 2060  PRINT AB%(L)
 2065  PRINT AC%(L)
 2070  PRINT AF%(L)
 2075  PRINT AG%(L)
 2080  PRINT AH%(L)
 2085  PRINT AI%(L)
 2090  PRINT AJ%(L)
 2095  PRINT AK%(L)
 2100  PRINT DV$(L)
 2105  PRINT DW$(L)
 2110  PRINT DX$(L)
 2115  PRINT WT%(L)
 2120  FOR M = 0 TO 7: PRINT AP$(L,M): NEXT 
 2125  FOR M = 0 TO 15: PRINT AX$(L,M): NEXT 
 2130  NEXT L
 2135  PRINT X
 2140  PRINT Y
 2145  PRINT AD%: PRINT AE
 2150  PRINT D$;"CLOSE D:";NA$
 2160  PRINT D$;"BSAVE MRDCK:";NA$;",A$8000,L3200
 2200  PRINT D$;"OPEN PLIST
 2210  PRINT D$;"WRITE PLIST
 2220  PRINT N
 2230  FOR I = 1 TO N
 2240  PRINT A$(I - 1)
 2250  NEXT 
 2260  PRINT D$;"CLOSE PLIST
 2500  PRINT D$;"BSAVE CK:";NA$;",A$6CF0,L$5
 2600  GOTO 80
 3000  HOME : CLEAR :D$ =  CHR$ (4)
 3001  ONERR  GOTO 11000
 3002  FOR I = 0 TO 9
 3003 A$(I) = "":B$(I) = ""
 3004  NEXT I
 3005  PRINT : PRINT : PRINT : PRINT D$;"VERIFY PLIST
 3006  PRINT D$;"OPEN PLIST
 3010  PRINT D$;"READ PLIST
 3020  INPUT N
 3030  FOR I = 1 TO N
 3040  INPUT A$(I - 1)
 3050  NEXT I
 3060  PRINT D$;"CLOSE PLIST
 3100  VTAB 1: HTAB 1: PRINT "NAME:"
 3160  FOR I = 1 TO N
 3170  VTAB I * 2 + 1
 3180  HTAB 1
 3185  IF A$(I - 1) = "" THEN A$(I - 1) = "EMPTY"
 3190  PRINT A$(I - 1)
 3230  NEXT I
 3240  PRINT : GET A$
 3250  GOTO 80
 4000  HOME : PRINT "NAME OF PARTY TO RENAME-": GOSUB 7000
 4001  IF A$ = "" THEN 80
 4002 NA$ = A$
 4003  HOME : PRINT "NEW NAME-": GOSUB 7000
 4004 B$ = A$
 4005  IF B$ = "" THEN 80
 4006  HOME : PRINT "WHICH VOLUME?": GOSUB 7000
 4010  ONERR  GOTO 10000
 4015  PRINT : PRINT : PRINT : PRINT  CHR$ (4);"VERIFY D:";NA$;",V";A$
 4016 A$ = B$
 4020  PRINT  CHR$ (4);"RENAME D:";NA$;",D:";A$
 4030  PRINT  CHR$ (4);"RENAME MRDCK:";NA$;",MRDCK:";A$
 4040  ONERR  GOTO 4070
 4050  PRINT  CHR$ (4);"VERIFY CK:";NA$
 4060  PRINT  CHR$ (4);"RENAME CK:";NA$;",CK:";A$
 4070 NN$ = A$
 4075  PRINT  CHR$ (4);"OPEN PLIST"
 4080  PRINT  CHR$ (4);"READ PLIST"
 4090  INPUT N
 4100  FOR I = 1 TO N
 4105  INPUT A$(I - 1)
 4110  NEXT I
 4120  PRINT  CHR$ (4);"CLOSE PLIST"
 4130 I = 1
 4140  IF A$(I - 1) = NA$ THEN 4160
 4150 I = I + 1: GOTO 4140
 4160 A$(I - 1) = NN$
 4170  PRINT  CHR$ (4);"OPEN PLIST"
 4180  PRINT  CHR$ (4);"WRITE PLIST"
 4190  PRINT N
 4200  FOR I = 1 TO N
 4210  PRINT A$(I - 1)
 4220  NEXT I
 4230  PRINT  CHR$ (4);"CLOSE PLIST"
 4240  GOTO 80
 5000  HOME : PRINT "NAME OF PARTY TO DELETE-": GOSUB 7000
 5005  IF A$ = "" THEN 80
 5006 NA$ = A$
 5007  HOME : PRINT "WHICH VOLUME?": GOSUB 7000
 5008 B$ = A$
 5009 A$ = NA$
 5010  ONERR  GOTO 10000
 5020  PRINT : PRINT : PRINT : PRINT  CHR$ (4);"VERIFY D:";A$;",V";B$
 5030  PRINT  CHR$ (4);"DELETE D:";A$
 5040  PRINT  CHR$ (4);"DELETE MRDCK:";A$
 5050  ONERR  GOTO 5080
 5060  PRINT  CHR$ (4);"VERIFY CK:";A$
 5070  PRINT  CHR$ (4);"DELETE CK:";A$
 5075 NA$ = A$
 5080  PRINT  CHR$ (4);"OPEN PLIST"
 5090  PRINT  CHR$ (4);"READ PLIST"
 5100  INPUT N
 5105  FOR I = 1 TO N
 5110  INPUT A$(I - 1)
 5120  NEXT I
 5130  PRINT  CHR$ (4);"CLOSE PLIST"
 5140 I = 1
 5150  IF A$(I - 1) = NA$ THEN 5170
 5160 I = I + 1: GOTO 5150
 5170 A$(I - 1) = ""
 5180  PRINT  CHR$ (4);"OPEN PLIST"
 5190  PRINT  CHR$ (4);"WRITE PLIST"
 5200  PRINT N
 5205  FOR I = 1 TO N
 5210  PRINT A$(I - 1)
 5220  NEXT I
 5230  PRINT  CHR$ (4);"CLOSE PLIST"
 5240  GOTO 80
 6000  HOME : PRINT "NOT APPLICABLE IN HD VERSION";: GET A$
 6010 A = 1
 6014  IF A > 2 THEN 6000
 6015  IF A < 1 THEN 6000
 6016 WV% = A: PRINT : GOTO 6019
 6018  HOME : PRINT "INSERT MASTER DISK- ";: GET A$
 6019  ONERR  GOTO 6018
 6020  PRINT : PRINT : PRINT : PRINT  CHR$ (4);"VERIFY TDISK,V30"
 6025  PRINT D$;"OPEN DD
 6026  PRINT D$;"WRITE DD
 6028  PRINT WV%
 6030  PRINT D$;"CLOSE DD
 6035  GOTO 80
 7000 A$ = "": HTAB 1: PRINT ">";
 7001  GET B$
 7002  IF B$ =  CHR$ (13) THEN  RETURN 
 7003  IF B$ =  CHR$ (8) THEN 7009
 7004  IF B$ =  CHR$ (10) OR B$ =  CHR$ (11) OR B$ =  CHR$ (21) THEN 7001
 7005  IF  LEN (A$) > 16 THEN 7001
 7006  PRINT B$;:A$ = A$ + B$: GOTO 7001
 7009  IF  LEN (A$) < 2 THEN 7000
 7010 A$ =  LEFT$ (A$, LEN (A$) - 1): POKE 36, PEEK (36) - 1: CALL  - 868: GOTO 7001
 10000  HOME : PRINT "'";NA$;"' NOT ON THIS DISK. ";: GET A$
 10010  GOTO 80
 11000  HOME : PRINT "INAPPROPRIATE DISK. ";: GET A$: GOTO 80
 12000  HOME : PRINT "NO ROOM ON THIS DISK. ";: GET A$: GOTO 80
 13000  HOME : PRINT "DISK ERROR!!!!!!";: GET A$: GOTO 80
