 10  PRINT CHR$(4);"BLOAD TITLE.PIC"
 20  HOME
 30  POKE  - 16304,0: POKE  - 16302,0: GET A$
 50  LOMEM: 27904
 60  HIMEM: 32767
 70  CLEAR : RESTORE
 80  GOSUB 8000 : REM GLOBAL ARRAYS
 90  GOSUB 6300 : REM DRIVE MAPPINGS
 100  TEXT : HOME
 110  PRINT "SELECT ONE-"
 120  PRINT "1) GENERATE A PARTY
 130  PRINT "2) COPY A PARTY
 140  PRINT "3) LIST EXISTING PARTIES
 150  PRINT "4) RENAME A PARTY
 160  PRINT "5) DELETE A PARTY
 170  PRINT "6) SET DISK DRIVES
 180  PRINT "7) ENTER THE GAME
 190  GET A$
 200 A =  VAL (A$)
 210  ON A GOTO 1000,2000,3000,4000,5000,6000,250
 220  GOTO 100

 250  REM ENTER THE GAME
 260  POKE 27893,0: POKE 27894,0: POKE 27895,0: REM ELEMENTAL ALTAR FLAGS
 270  D = 3 : GOSUB 400 : GOSUB 500 : REM LOAD PARTY
 280  D = 0 : GOSUB 9000
 290  PRINT : PRINT : PRINT : PRINT CHR$(4);"BLOAD OUTSIDE,A$4000"
 299  POKE 103,0: POKE 104,64: GOTO 0

 400  REM PICK PARTY IN D
 410  HOME : PRINT "ENTER THE PARTY'S NAME-": GOSUB 7000
 420  NA$ = A$: IF NA$ = "" THEN 450
 430  GOSUB 9000 : GOSUB 10000
 440  RETURN
 450  CALL -3288
 460  GOTO 100

 500  REM LOAD
 530  PRINT : PRINT : PRINT : PRINT CHR$(4);"OPEN D:";NA$
 535  PRINT CHR$(4);"READ D:";NA$
 540  FOR L = 0 TO 3
 541  INPUT NM$(L)
 542  INPUT RA$(L)
 543  INPUT CL$(L)
 544  INPUT ZA%(L)
 545  INPUT AB%(L)
 546  INPUT AC%(L)
 547  INPUT AF%(L)
 548  INPUT AG%(L)
 548  INPUT AH%(L)
 549  INPUT AI%(L)
 550  INPUT AJ%(L)
 551  INPUT AK%(L)
 552  INPUT DV$(L)
 553  INPUT DW$(L)
 554  INPUT DX$(L)
 555  INPUT WT%(L)
 556  FOR M = 0 TO 7: INPUT AP$(L,M): NEXT
 557  FOR M = 0 TO 15: INPUT AX$(L,M): NEXT
 560  NEXT L
 565  INPUT X
 566  INPUT Y
 570  INPUT AD%: INPUT AE: INPUT P$
 575  PRINT CHR$(4);"CLOSE D:";NA$
 580  PRINT CHR$(4);"BLOAD CK:";NA$;",A$6CF0"
 585  PRINT CHR$(4);"BLOAD MAP:";NA$;",A$8000"
 600  FOR L = 0 TO 3:WU(L) = 1
 610  IF CL$(L) = "WIZARD" THEN WU(L) = 2
 620  IF CL$(L) = "CLERIC" THEN WU(L) = 1.8
 630  IF CL$(L) = "THIEF" THEN WU(L) = 1.5
 640  IF CL$(L) = "MONK" THEN WU(L) = 1.3
 650  IF CL$(L) = "RANGER" THEN WU(L) = 1.3
 660  IF CL$(L) = "PALADIN" THEN WU(L) = 1.1
 670  NEXT L
 680  FOR I = 0 TO 8: TW$(I) = TW$(I) +  CHR$ (0): NEXT
 699  RETURN

 700  REM SAVE
 705  PRINT : PRINT : PRINT : PRINT CHR$(4);"OPEN D:";NA$
 710  PRINT CHR$(4);"WRITE D:";NA$
 715  FOR L = 0 TO 3
 720  PRINT NM$(L)
 725  PRINT RA$(L)
 730  PRINT CL$(L)
 735  PRINT ZA%(L)
 740  PRINT AB%(L)
 745  PRINT AC%(L)
 750  PRINT AF%(L)
 755  PRINT AG%(L)
 760  PRINT AH%(L)
 765  PRINT AI%(L)
 770  PRINT AJ%(L)
 775  PRINT AK%(L)
 780  PRINT DV$(L)
 785  PRINT DW$(L)
 790  PRINT DX$(L)
 795  PRINT WT%(L)
 800  FOR M = 0 TO 7: PRINT AP$(L,M): NEXT
 805  FOR M = 0 TO 15: PRINT AX$(L,M): NEXT
 810  NEXT L
 815  PRINT X
 820  PRINT Y
 825  PRINT AD%: PRINT AE: PRINT P$
 830  PRINT CHR$(4);"CLOSE D:";NA$
 835  PRINT CHR$(4);"BSAVE CK:";NA$;",A$6CF0,L$5"
 840  PRINT CHR$(4);"BSAVE MAP:";NA$;",A$8000,L3200"
 850  RETURN

 900  REM LOAD LIST
 905  PRINT : PRINT : PRINT : PRINT CHR$(4);"OPEN PLIST"
 910  PRINT CHR$(4);"READ PLIST"
 925  FOR I = 0 TO 9
 930  INPUT PN$(I)
 935  NEXT I
 940  PRINT CHR$(4);"CLOSE PLIST"
 949  RETURN

 950  REM SAVE LIST
 955  PRINT : PRINT : PRINT : PRINT CHR$(4);"OPEN PLIST"
 960  PRINT CHR$(4);"WRITE PLIST"
 975  FOR I = 0 TO 9
 980  PRINT PN$(I)
 985  NEXT I
 990  PRINT CHR$(4);"CLOSE PLIST"
 999  RETURN

 1000  REM CREATE
 1010  D = 3 : GOSUB 9000
 1020  GOSUB 900
 1030  I = 0
 1040  IF PN$(I) = "" THEN 1090
 1050  I = I + 1 : IF I = 10 THEN 1080
 1060  GOTO 1040
 1080  HOME: PRINT "THERE IS NO ROOM": GET A$: GOTO 100
 1090  PRINT  CHR$ (4);"RUN CREATE"

 2000  REM COPY
 2010  HOME : PRINT "INSERT DISK TO COPY FROM- ": GET A$
 2020  D = 3 : GOSUB 400 : GOSUB 500
 2030  PRINT "INSERT DISK TO COPY TO- ": GET A$
 2040  D = 3 : GOSUB 9000 : GOSUB 900
 2050  I = 0 : REM PARTY INDEX
 2060  IF PN$(I) = "" THEN 2090
 2070  I = I + 1: IF I = 10 THEN 12000
 2080  GOTO 2060
 2090  PN$(I) = NA$ : GOSUB 950 : REM SAVE PARTY LIST
 2100  GOSUB 700 : REM SAVE PARTY DATA
 2110  GOTO 100

 3000  REM LIST
 3010  D = 3 : GOSUB 9000 : GOSUB 900
 3020  HOME: VTAB 1: HTAB 1: PRINT "NAME:"
 3030  FOR I = 1 TO 10
 3040  VTAB I * 2 + 1
 3050  HTAB 1
 3060  IF PN$(I - 1) = "" THEN PN$(I - 1) = "EMPTY"
 3070  PRINT PN$(I - 1)
 3080  NEXT I
 3090  PRINT : GET A$ : GOTO 100

 4000  REM RENAME
 4001  HOME : PRINT "NAME OF PARTY TO RENAME-": GOSUB 7000
 4002  IF A$ = "" THEN 100
 4003 NA$ = A$
 4004  HOME : PRINT "NEW NAME-": GOSUB 7000
 4005 B$ = A$
 4006  IF B$ = "" THEN 100
 4010  D = 3 : GOSUB 9000 : GOSUB 10000
 4020  PRINT : PRINT : PRINT : PRINT  CHR$ (4);"RENAME D:";NA$;",D:";B$
 4030  PRINT  CHR$ (4);"RENAME MAP:";NA$;",MAP:";B$
 4040  PRINT  CHR$ (4);"RENAME CK:";NA$;",CK:";B$
 4050  GOSUB 900 : REM LOAD PARTY LIST
 4060  I = 0
 4070  IF PN$(I) = NA$ THEN 4090
 4080  I = I + 1: GOTO 4070
 4090  PN$(I) = B$
 4095  GOSUB 950 : REM SAVE PARTY LIST
 4099  GOTO 100

 5000  REM DELETE
 5010  HOME : PRINT "NAME OF PARTY TO DELETE-": GOSUB 7000
 5020  IF A$ = "" THEN 100
 5030  NA$ = A$
 5040  D = 3 : GOSUB 9000 : GOSUB 10000
 5050  PRINT "ARE YOU SURE (Y TO PROCEED)- ": GET A$
 5060  IF A$ <> "Y" THEN 100
 5030  PRINT : PRINT : PRINT : PRINT  CHR$ (4);"DELETE D:";NA$
 5040  PRINT  CHR$ (4);"DELETE MAP:";NA$
 5070  PRINT  CHR$ (4);"DELETE CK:";NA$
 5080  GOSUB 900 : REM LOAD PARTY LIST
 5090  I = 0
 5100  IF PN$(I) = NA$ THEN 5120
 5110  I = I + 1: GOTO 5100
 5120  PN$(I) = ""
 5130  GOSUB 950 : REM SAVE PARTY LIST
 5140  GOTO 100

 6000  REM DRIVE MAPPINGS
 6010  HOME
 6020  PRINT TAB(10);"SETUP DRIVE MAPPINGS"
 6030  PRINT : PRINT: PRINT
 6040  PRINT "DISK NAME  SLOT   DRIVE  VOLUME"
 6050  PRINT "-------------------------------"
 6060  FOR I = 0 TO 3
 6070  A$ = DL$(I) + "          ": A$ = LEFT$(A$,11)
 6080  PRINT A$;WS%(I);"      ";WD%(I);"      ";WV%(I)
 6090  NEXT I
 6100  PRINT "ENTER DISK NAME, 'FINISH', OR 'CANCEL' -": GOSUB 7000: PRINT
 6101  IF A$=DL$(0) OR A$=DL$(1) OR A$=DL$(2) OR A$=DL$(3) OR A$="FINISH" OR A$="CANCEL" THEN 6110
 6102  INPUT "RESPONSE NOT VALID. PRESS ENTER.";A$
 6103  GOTO 6000
 6110  IF A$="FINISH" THEN 6170
 6111  IF A$="CANCEL" THEN 100
 6120  IF A$=DL$(0) THEN I=0
 6121  IF A$=DL$(1) THEN I=1
 6122  IF A$=DL$(2) THEN I=2
 6123  IF A$=DL$(3) THEN I=3
 6130  INPUT "ENTER SLOT (1-7): ";B$
 6131  WS%(I) = VAL(B$): IF WS%(I)<1 OR WS%(I)>7 THEN 6130
 6140  INPUT "ENTER DRIVE (1-7): ";B$
 6141  WD%(I) = VAL(B$): IF WD%(I)<1 OR WD%(I)>9 THEN 6140
 6150  INPUT "ENTER VOLUME (1-255, USUALLY 254): ";B$
 6151  WV%(I) = VAL(B$): IF WV%(I)<1 OR WV%(I)>255 THEN 6150
 6160  GOTO 6000
 6170  D = 0: GOSUB 9000: GOSUB 6200
 6180  D = 3: GOSUB 9000: GOSUB 6200
 6190  GOTO 100

 6200  REM WRITE DRIVES
 6210  PRINT : PRINT : PRINT : PRINT CHR$(4);"OPEN DD
 6220  PRINT CHR$(4);"WRITE DD
 6230  FOR I = 0 TO 3
 6240  PRINT DL$(I)
 6250  PRINT WS%(I)
 6260  PRINT WD%(I)
 6270  PRINT WV%(I)
 6280  NEXT I
 6290  PRINT CHR$(4);"CLOSE DD
 6299  RETURN

 6300  REM READ DRIVES
 6310  PRINT : PRINT : PRINT : PRINT CHR$(4);"OPEN DD"
 6320  PRINT CHR$(4);"READ DD"
 6330  FOR I = 0 TO 3
 6340  INPUT DL$(I)
 6350  INPUT WS%(I)
 6360  INPUT WD%(I)
 6370  INPUT WV%(I)
 6380  NEXT I
 6390  PRINT CHR$(4);"CLOSE DD"
 6399  RETURN

 7000  REM INPUT
 7010 A$ = "": HTAB 1: PRINT ">";
 7020  GET B$
 7030  IF B$ =  CHR$ (13) THEN  RETURN
 7040  IF B$ =  CHR$ (8) THEN 7080
 7050  IF B$ =  CHR$ (10) OR B$ =  CHR$ (11) OR B$ =  CHR$ (21) THEN 7020
 7060  IF  LEN (A$) > 16 THEN 7020
 7070  PRINT B$;:A$ = A$ + B$: GOTO 7020
 7080  IF  LEN (A$) < 2 THEN 7000
 7090 A$ =  LEFT$ (A$, LEN (A$) - 1): POKE 36, PEEK (36) - 1: CALL  - 868: GOTO 7010

 8000  REM GLOBALS
 8010  DIM PN$(9)
 8020  DIM WS%(3): DIM WD%(3): DIM WV%(3): REM SLOT DRIVE VOLUME
 8030  DIM DL$(3): REM DISK LABELS
 8040  DIM TW$(8): DIM WU(3):
 8050  DIM NM$(3): DIM RA$(3)
 8060  DIM CL$(3): DIM ZA%(3)
 8070  DIM AB%(3): DIM AC%(3)
 8080  DIM AF%(3): DIM AG%(3)
 8090  DIM AH%(3): DIM AI%(3)
 8100  DIM AJ%(3): DIM AK%(3)
 8110  DIM DV$(3): DIM DW$(3)
 8120  DIM DX$(3): DIM WT%(3)
 8130  DIM B$(3): DIM PM%(9)
 8140  DIM PC%(3): DIM GP%(3)
 8150  DIM AX$(3,15): DIM AP$(3,7)
 8160 TW$(0) = "GUARD":TW$(1) = "HUMAN":TW$(2) = "ELF"
 8170 TW$(3) = "DWARF":TW$(4) = "HOBBIT":TW$(5) = "ADVENTURER"
 8180 TW$(6) = "WIZARD":TW$(7) = "MARINE":TW$(8) = "DRAGON"
 8190  REM POKE IN STACK FIXER
 8191  POKE 768,104 : POKE 769,168 : POKE 770,104 : POKE 771,166 : POKE 772,223 : POKE 773,154 : POKE 774,72 : POKE 775,152 : POKE 776,72 : POKE 777,96
 8199  RETURN

 9000  REM VERIFY DISK
 9110  ONERR  GOTO 9200
 9120  PRINT : PRINT : PRINT
 9130  PRINT  CHR$(4);"VERIFY DISK";D;",S";WS%(D);",D";WD%(D);",V";WV%(D)
 9140  POKE 216,0 : RETURN
 9200  HOME : PRINT "INSERT ";DL$(D);" DISK IN D";WD%(D);"- ";: GET A$
 9210  CALL 768
 9220  PRINT : PRINT : PRINT : RESUME

 10000  REM VERIFY PARTY
 10110  ONERR  GOTO 10200
 10120  PRINT : PRINT : PRINT
 10130  PRINT  CHR$(4);"VERIFY CK:";NA$
 10140  RETURN
 10200  HOME : PRINT "'";NA$;"' NOT ON THIS DISK. ";: GET A$
 10210  CALL -3288: REM CLEAR THE STACK
 10220  POKE 216,0 : GOTO 100

 12000  HOME : PRINT "NO ROOM ON THIS DISK. ";: GET A$: GOTO 100
