10 ON I GOTO 11,12,13,14,15,16,17,18
11 C = 50: B$ = "HEAL": RETURN
12 C = 500: B$ = "RESURRECT": RETURN
13 C = 200: B$ = "POTION OF HEALING": RETURN
14 C = 200: B$ = "POTION OF PASSAGE": RETURN
15 C = 200: B$ = "POTION OF INFURIATION": RETURN
16 C = 500: B$ = "POTION OF INTIMIDATION": RETURN
17 C = 1000: B$ = "POTION OF VISION": RETURN
18 C = 4000: B$ = "FLYING CARPET": RETURN

4210  PRINT NM$(A);"'S ITEMS:": FOR I = 0 TO 7: PRINT I + 1;") ";: PRINT AP$(A,I): NEXT: RETURN

4300  PRINT "FOR SALE:"
4330  FOR I = 1 TO 8: GOSUB 10: GOSUB 19000: NORMAL: IF I=1 THEN INVERSE
4340  PRINT B$;: NORMAL: PRINT " (";C;")": NEXT: RETURN

4400  N = 99: IF A$ = CHR$(8) THEN A = A - 1: IF A<0 THEN A = 3
4410  IF A$ = CHR$(21) THEN A = A + 1: IF A>3 THEN A = 0
4420  IF A$ = "A" OR A$ = CHR$(11) THEN N = M: M = M - 1: IF M<0 THEN M = P
4430  IF A$ = "Z" OR A$ = CHR$(10) THEN N = M: M = M + 1: IF M>P THEN M = 0
4440  RETURN

4450  HTAB 1: VTAB I+9: PRINT "LR-ARROWS=CHARACTER, ENTER=PLACE": PRINT "UD-ARROWS/A/Z=SLOT, ESC=CANCEL"
4460  HTAB 4: VTAB N+I: PRINT AP$(A,N);" ": INVERSE: HTAB 4: VTAB M+I: PRINT AP$(A,M);" ": NORMAL
4461  HTAB 34: VTAB I+10: GET A$: IF A$ = CHR$(27) THEN M = 999: RETURN
4470  IF A$ = CHR$(13) THEN RETURN
4480  GOSUB 4400: IF N = 99 THEN M = 99: RETURN
4490  GOTO 4460

4600  TEXT: HOME: PRINT "PLACING: ";B$: PRINT: GOSUB 4210: M = 0: N = 0: P = 7: I = 4: GOSUB 4450: IF M = 999 THEN PRINT: PRINT "SALE ABORTED! ";: GET A$: RETURN
4610  IF M = 99 THEN 4600
4620  IF AP$(A,M) = "" THEN 4650
4630  HOME: PRINT "REPLACE ";AP$(A,M);" (Y/N) ? ";: GET A$: IF A$ <> "Y" AND A$ <> "N" THEN 4630
4640  IF A$ = "N" THEN 4600
4650  AP$(A,M) =  LEFT$ (B$ + " ", LEN (B$)): AD% = AD% - C: RETURN

7750  PRINT: PRINT "-HEALING THE PARTY-": GOSUB 30000
7752  FOR I = 0 TO 3: IF AB%(I)>0 THEN AB%(I) = AC%(I)
7753  NEXT
7754  PRINT "PARTY IS HEALED! ";: GET A$: GOTO 9510

7760  M = 1: FOR I = 0 TO 3: M = M * AB%(I): NEXT I
7761  IF M > 0 THEN PRINT: AD% = AD% + C: PRINT "NO ONE IS DEAD! ";: GOSUB 30050: GOTO 9510
7765  PRINT: PRINT "-RESURRECTING-": GOSUB 30000
7766  FOR I = 0 TO 3
7767  IF AB%(I) = 0 THEN AB%(I) = AC%(I): PRINT NM$(I);" IS RAISED!"
7768  NEXT I
7769  GET A$: GOTO 9510

8900  GET A$
9510  TEXT: HOME: PRINT "WELCOME TO THE ALCHEMIST:"
9520  PRINT: GOSUB 4300: I = 1
9530  HTAB 1: VTAB 13: PRINT "GOLD: ";AD%;"- ARROWS, ENTER, ESC: ";: GET A$: IF A$ = CHR$(27) THEN 25000
9540  L = 0: IF A$ = CHR$(13) THEN GOSUB 10: GOSUB 19000: GOTO 9560
9550  IF A$ = CHR$(8) OR A$ = CHR$(11) THEN L = -1
9551  IF A$ = CHR$(10) OR A$ = CHR$(21) THEN L = 1
9552  GOSUB 10: HTAB 1: VTAB 3+I: PRINT B$: I = I + L
9553  IF I<1 THEN I=8
9554  IF I>8 THEN I=1
9555  GOSUB 10: HTAB 1: VTAB 3+I: INVERSE: PRINT B$: NORMAL: GOTO 9530

9560  GOSUB 19000: IF AD% < C THEN PRINT: PRINT "YOU DON'T HAVE THE GOLD! ";: GOSUB 30050: GOTO 9510
9561  AD% = AD% - C
9562  IF B$ = "HEAL" THEN 7750
9563  IF B$ = "RESURRECT" THEN 7760
9570  AD% = AD% + C: A = 0: GOSUB 4600: GOTO 9510

19000 C = INT (C * (1 - (AK%(0)+AK%(1)+AK%(2)+AK%(3)) / 400)): RETURN

25000  L=1: POKE -16304,0: POKE 16384,0: POKE 103,1: POKE 104,64: GOTO 25000

30000  POKE 6,64: POKE 7,2: POKE 8,100: POKE 9,1: POKE 30,2: CALL 806: RETURN
30050  POKE 6,50: POKE 7,25: POKE 8,1: POKE 9,1: POKE 30,1: CALL 852: GET A$: RETURN
