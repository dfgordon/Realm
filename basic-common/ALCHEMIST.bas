1 C = INT (C * (1 - (AK%(0)+AK%(1)+AK%(2)+AK%(3)) / 400)): RETURN

2 ON I GOTO 3,4,5,6,7,8,9,10
3 C = 50: B$ = "HEAL": RETURN
4 C = 500: B$ = "RESURRECT": RETURN
5 C = 200: B$ = "HEALING": GOTO 11
6 C = 200: B$ = "PASSAGE": GOTO 11
7 C = 200: B$ = "INFURIATION": GOTO 11
8 C = 500: B$ = "INTIMIDATION": GOTO 11
9 C = 1000: B$ = "VISION": GOTO 11
10 C = 4000: B$ = "FLYING CARPET": RETURN
11 B$ = "POTION OF " + B$: RETURN

12 PRINT "FOR SALE:"
13 FOR I = 1 TO 8: GOSUB 2: GOSUB 1: NORMAL: IF I=1 THEN INVERSE
14 PRINT B$;: NORMAL: PRINT " (";C;")": NEXT: RETURN

21 PRINT NM$(A);"'S ITEMS:": FOR I = 0 TO 7: PRINT I + 1;") ";: PRINT AP$(A,I): NEXT: RETURN

22 N = 99: IF A$ = CHR$(8) THEN A = A - 1: IF A<0 THEN A = 3
23 IF A$ = CHR$(21) THEN A = A + 1: IF A>3 THEN A = 0
24 IF A$ = "A" OR A$ = CHR$(11) THEN N = M: M = M - 1: IF M<0 THEN M = P
25 IF A$ = "Z" OR A$ = CHR$(10) THEN N = M: M = M + 1: IF M>P THEN M = 0
26 RETURN

27 HTAB 1: VTAB I+9: PRINT "LR-ARROWS=CHARACTER, ENTER=PLACE": PRINT "UD-ARROWS/A/Z=SLOT, ESC=CANCEL"
28 HTAB 4: VTAB N+I: PRINT AP$(A,N);" ": INVERSE: HTAB 4: VTAB M+I: PRINT AP$(A,M);" ": NORMAL
29 HTAB 34: VTAB I+10: GET A$: IF A$ = CHR$(27) THEN M = 999: RETURN
30 IF A$ = CHR$(13) THEN RETURN
31 GOSUB 22: IF N = 99 THEN M = 99: RETURN
32 GOTO 28

33 TEXT: HOME: PRINT "PLACING: ";B$: PRINT: GOSUB 21: M = 0: N = 0: P = 7: I = 4: GOSUB 27: IF M = 999 THEN PRINT: PRINT "SALE ABORTED! ";: GET A$: RETURN
34 IF M = 99 THEN 33
35 IF AP$(A,M) = "" THEN 38
36 HOME: PRINT "REPLACE ";AP$(A,M);" (Y/N) ? ";: GET A$: IF A$ <> "Y" AND A$ <> "N" THEN 36
37 IF A$ = "N" THEN 33
38 AP$(A,M) =  LEFT$ (B$ + " ", LEN (B$)): AD% = AD% - C: GOSUB 39: RETURN

39 M = 8: N = 9: FOR I = 7 TO 0 STEP -1: M = M*(DW$(A) <> AP$(A,I)) + I*(DW$(A) = AP$(A,I)): N = N*(DV$(A) <> AP$(A,I)) + I*(DV$(A) = AP$(A,I)): NEXT
40 DW$(A) = AP$(A,M): DR%(A,0) = M: DV$(A) = AP$(A,N): DR%(A,1) = N: RETURN

41 PRINT: PRINT "-HEALING-": GOSUB 62
42 FOR I = 0 TO 3: IF AB%(I)>0 THEN AB%(I) = AC%(I)
43 NEXT: PRINT "PARTY IS HEALED! ";: GET A$: GOTO 48

44 M = 1: FOR I = 0 TO 3: M = M * AB%(I): NEXT I
45 IF M > 0 THEN PRINT: AD% = AD% + C: PRINT "NO ONE IS DEAD! ";: GOSUB 63: GOTO 48
46 PRINT: PRINT "-RESURRECTING-": GOSUB 62: FOR I = 0 TO 3: IF AB%(I) = 0 THEN AB%(I) = AC%(I): PRINT NM$(I);" IS RAISED!"
47 NEXT: GET A$: GOTO 48

48 TEXT: HOME: PRINT "WELCOME TO THE ALCHEMIST!"
49 PRINT: GOSUB 12: I = 1
50 HTAB 1: VTAB 13: PRINT "GOLD: ";AD%;"- ARROWS, ENTER, ESC: ";: GET A$: IF A$ = CHR$(27) THEN 25000
51 L = 0: IF A$ = CHR$(13) THEN GOSUB 2: GOTO 58
52 IF A$ = CHR$(8) OR A$ = CHR$(11) THEN L = -1
53 IF A$ = CHR$(10) OR A$ = CHR$(21) THEN L = 1
54 GOSUB 2: HTAB 1: VTAB 3+I: PRINT B$: I = I + L
55 IF I<1 THEN I=8
56 IF I>8 THEN I=1
57 GOSUB 2: HTAB 1: VTAB 3+I: INVERSE: PRINT B$: NORMAL: GOTO 50

58 GOSUB 1: IF AD% < C THEN PRINT: PRINT "NOT ENOUGH GOLD! ";: GOSUB 63: GOTO 48
59 AD% = AD% - C: IF B$ = "HEAL" THEN 41
60 IF B$ = "RESURRECT" THEN 44
61 AD% = AD% + C: A = 0: GOSUB 33: GOTO 48

62 POKE 6,64: POKE 7,2: POKE 8,100: POKE 9,1: POKE 30,2: CALL 806: RETURN
63 POKE 6,50: POKE 7,25: POKE 8,1: POKE 9,1: POKE 30,1: CALL 852: GET A$: RETURN

8900 GET A$: GOTO 48

25000 L=1: POKE -16304,0: POKE 16384,0: POKE 103,1: POKE 104,64: GOTO 25000
