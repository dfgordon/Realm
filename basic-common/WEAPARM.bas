1 C = INT (C * (1 - (AK%(0)+AK%(1)+AK%(2)+AK%(3)) / 400)): RETURN

2 ON I GOTO 3,4,5,6,7,8,9,10,11,12,13,14,15
3 C = 30: B$ = "DAGGER": RETURN
4 C = 80: B$ = "AXE": RETURN
5 C = 80: B$ = "MACE": RETURN
6 C = 100: B$ = "MORNING STAR": RETURN
7 C = 120: B$ = "BOW": RETURN
8 C = 200: B$ = "LONGSWORD": RETURN
9 C = 300: B$ = "BATTLEAXE": RETURN
10 C = 340: B$ = "BROADSWORD": RETURN
11 C = 500: B$ = "2H SWORD": RETURN
12 C = 2000: B$ = "SWORD OF LIGHT": RETURN
13 C = 90: B$ = "LEATHER": RETURN
14 C = 180: B$ = "CHAIN": RETURN
15 C = 400: B$ = "PLATE": RETURN

16 PRINT "FOR SALE:"
17 P = 1: N = 9: IF MA = 15 THEN P = 11: N = 13
18 IF T$="SULPHUR CITY" AND MA = 10 THEN P = 1: N = 10
19 FOR I = P TO N: GOSUB 2: GOSUB 1: NORMAL: IF I=P THEN INVERSE
20 PRINT B$;: NORMAL: PRINT " (";C;")": NEXT: RETURN

21 PRINT NM$(A);"'S ITEMS:": FOR I = 0 TO 7: PRINT I + 1;") ";: PRINT AP$(A,I): NEXT: RETURN

22 N = 99: IF A$ = CHR$(8) THEN A = A - 1: IF A<0 THEN A = 3
23 IF A$ = CHR$(21) THEN A = A + 1: IF A>3 THEN A = 0
24 IF A$ = "A" OR A$ = CHR$(11) THEN N = M: M = M - 1: IF M<0 THEN M = P
25 IF A$ = "Z" OR A$ = CHR$(10) THEN N = M: M = M + 1: IF M>P THEN M = 0
26 RETURN

27 HTAB 1: VTAB I+9: PRINT "LR-ARROWS=CHARACTER, ENTER=PLACE": PRINT "UD-ARROWS/A/Z=SLOT, ESC=CANCEL"
28 HTAB 4: VTAB N+I: PRINT AP$(A,N);" ": INVERSE: HTAB 4: VTAB M+I: PRINT AP$(A,M);" ": NORMAL
29 HTAB 34: VTAB I+10: GET A$: IF A$ = CHR$(27) THEN M = 999: RETURN
30 IF A$ = CHR$(13) THEN RETURN
31 GOSUB 22: IF N = 99 THEN M = 99: RETURN
32 GOTO 28

33 TEXT: HOME: PRINT "PLACING: ";B$: PRINT: GOSUB 21: M = 0: N = 0: P = 7: I = 4: GOSUB 27: IF M = 999 THEN PRINT: PRINT "SALE ABORTED! ";: GET A$: RETURN
34 IF M = 99 THEN 33
35 IF AP$(A,M) = "" THEN 38
36 HOME: PRINT "REPLACE ";AP$(A,M);" (Y/N) ? ";: GET A$: IF A$ <> "Y" AND A$ <> "N" THEN 36
37 IF A$ = "N" THEN 33
38 AP$(A,M) =  LEFT$ (B$ + " ", LEN (B$)): AD% = AD% - C: GOSUB 39: RETURN

39 M = 8: N = 9: FOR I = 7 TO 0 STEP -1: M = M*(DW$(A) <> AP$(A,I)) + I*(DW$(A) = AP$(A,I)): N = N*(DV$(A) <> AP$(A,I)) + I*(DV$(A) = AP$(A,I)): NEXT
40 DW$(A) = AP$(A,M): DR%(A,0) = M: DV$(A) = AP$(A,N): DR%(A,1) = N: RETURN

41 TEXT: HOME: PRINT "WELCOME TO THE ";
42 IF MA = 10 THEN PRINT "WEAPONS SHOP!"
43 IF MA = 15 THEN PRINT "ARMOURY!"
44 PRINT: GOSUB 16: I = P
45 HTAB 1: VTAB 6+N-P: PRINT "GOLD: ";AD%;"- ARROWS, ENTER, ESC: ";: GET A$: IF A$ = CHR$(27) THEN 25000
46 L = 0: IF A$ = CHR$(13) THEN GOSUB 2: GOSUB 1: GOTO 53
47 IF A$ = CHR$(8) OR A$ = CHR$(11) THEN L = -1
48 IF A$ = CHR$(10) OR A$ = CHR$(21) THEN L = 1
49 GOSUB 2: HTAB 1: VTAB 4+I-P: PRINT B$: I = I + L
50 IF I<P THEN I=N
51 IF I>N THEN I=P
52 GOSUB 2: HTAB 1: VTAB 4+I-P: INVERSE: PRINT B$: NORMAL: GOTO 45

53 IF AD% < C THEN PRINT: PRINT "YOU DON'T HAVE THE GOLD! ";: GOSUB 55: GOTO 41
54 A = 0: GOSUB 33: GOTO 41

55 POKE 6,50: POKE 7,25: POKE 8,1: POKE 9,1: POKE 30,1: CALL 852: GET A$: RETURN

8900 MA = M: GOTO 41

25000 L=1: POKE -16304,0: POKE 16384,0: POKE 103,1: POKE 104,64: GOTO 25000
