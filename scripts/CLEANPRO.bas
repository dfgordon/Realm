10  REM CLEAN FOR PRODOS DISTRIBUTION
20  DIM PN$(9)
30  GOSUB 900
40  FOR I = 0 TO 9
50  IF PN$(I) <> "EMPTY" THEN GOSUB 800
60  NEXT I

100  PRINT "RESET CONFIGURATION"
110  GOSUB 5300
120  PRINT CHR$(4);"DELETE REALM/DD"
140  ES = 100: EP = 100
150  GOSUB 5200
160  END

800  PRINT "REMOVING PARTY ";PN$(I)
810  PRINT CHR$(4);"DELETE REALM/PARTIES/D.";PN$(I)
820  PRINT CHR$(4);"DELETE REALM/PARTIES/AR.";PN$(I)
830  PRINT CHR$(4);"DELETE REALM/PARTIES/AB.";PN$(I)
840  PRINT CHR$(4);"DELETE REALM/PARTIES/FO.";PN$(I)
850  PRINT CHR$(4);"DELETE REALM/PARTIES/WO.";PN$(I)
860  RETURN

900  REM LOAD LIST
901  FOR I = 0 TO 9: PN$(I) = "EMPTY": NEXT I
905  PRINT : PRINT CHR$(4);"OPEN REALM/PARTIES,TDIR"
910  PRINT CHR$(4);"READ REALM/PARTIES"
911  INPUT VN$: INPUT A$: INPUT A$: REM STRIP HEADER
920  I = 0
925  INPUT A$: IF A$ = "" THEN 940
930  GOSUB 950: IF I>8 THEN 940
935  GOTO 925
940  PRINT CHR$(4);"CLOSE REALM/PARTIES"
949  RETURN

950  REM EXTRACT PARTY NAME
960  IF MID$(A$,2,2) <> "D." THEN RETURN
970  M = 4
980  IF MID$(A$,M,1) = " " OR M = 17 THEN PN$(I) = MID$(A$,4,M-4): I = I + 1: RETURN
990  M = M + 1: GOTO 980

5200  REM WRITE CONFIG
5210  PRINT : PRINT CHR$(4);"OPEN REALM/DD"
5220  PRINT CHR$(4);"WRITE REALM/DD"
5281  PRINT ES
5282  PRINT EP
5290  PRINT CHR$(4);"CLOSE REALM/DD"
5299  RETURN

5300  REM READ CONFIG
5310  PRINT : PRINT CHR$(4);"OPEN REALM/DD"
5320  PRINT CHR$(4);"READ REALM/DD"
5381  INPUT ES
5382  INPUT EP
5390  PRINT CHR$(4);"CLOSE REALM/DD"
5399  RETURN
